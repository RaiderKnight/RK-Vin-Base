WHAT: all, -sleep, -nap, -sleep_hotel, -nap_hotel
WHERE: home
WHEN: 16 - 22
WHO: none
OTHER: !isWithCompanion() && jobexperience < 40 && age < 30

    SceneStart()
    SugarParent = SugarParent.getGlobal()    
    If SugarParent > 0
        Actor = getSpecific(SugarParent)
        "My phone is ringing. It's my <Actor.sugardaddy_or_sugarmommy> <Actor.name>."
        secondScreen(Actor)
        Actor.dress()
        Actor.show(2)
        Actor(Happy):: "Honey, I miss you. Let's meet tonight."
        0:: "Yes"
        1:: "No"
        2:: "End arrangement"
        
        If 0
            Player(Happy):: "I miss you too, <Actor.Daddy_or_Mommy>. Let's meet."
            secondScreen()
            If Random(0, 100) < 50
                Loc = findNearbyBuilding(restaurant, bar, nightclub)
                moveTo(Loc)
                Actor.show()
                "With that said, that evening, <Actor.Daddy_or_Mommy> took me to a nice expensive date and as usual, we ended up in luxury hotel suite afterwards."
            Else
                Loc = findNearbyBuilding(clothes, shoes, bag)
                moveTo(Loc)
                Actor.show()
                "With that said, that evening, <Actor.Daddy_or_Mommy> took me shopping at the finest designer brand on the high street."
                Player(Sad):: "Damn, that looks so good ... but it's too expensive. <Actor.Daddy_or_Mommy>, would you be so kind as to ..."
                Actor(Happy):: "No problem, baby. That price tag is really not much money for me at all and I would love nothing more than to be able to spoil a <Player.handsome_or_beautiful> <Player.prince_or_princess> like you ..."
                "And just like that, <Actor.he_or_she> went ahead to buy me what I wanted, seemingly unmoved by a small fortune that thing cost. I guess expensive gifts like this are part and parcel of the sugardating scene."
                "Of course, as usual, we ended up in luxury hotel suite afterwards."            
            Endif
            Loc = findNearbyBuilding(hotel)
            moveTo(Loc)
            setBackground(home)
            Threesome = false
            If Random(30, 200) < Actor:perversion
                "Once we were there, I was surprised to see that some stranger was already waiting in the hotel suite ..."
                Actor2 = generatePersonTemporary()
                Actor2.dress()
                Actor2.show(2)
                Actor(Happy):: "Love, would you mind us sharing you tonight? This is <Actor2.name>, one of my most important business partners."
                0:: "Anything for you, <Actor.Daddy_or_Mommy>"
                1:: "No way"
                
                If 0
                    Threesome = true
                    Player(Flirty):: "You know I'll do anything for you as long as you remain so generous to me ..."
                    Actor(Flirty):: "That's my good <Player.boy_or_girl>."
                Else
                    Threesome = false
                    Player(Sad):: "I'm sorry, <Actor.Daddy_or_Mommy>. But this isn't part of our arrangement ..."
                    Actor(Angry):: "Fine then ... I won't force you."
                Endif
            Endif
            If Threesome
                Sex(Actor, Player, Actor2)
                Actor.show(2)
                Actor2.hide()
                perversion += Random(0, 0.5)
            Else
                Sex(Actor, Player)
                Actor.show(2)            
            Endif
            "With the deed done, we spent the night together and <Actor.Daddy_or_Mommy> left me a big fat envelope in the morning. Getting pampered and spoiled, having great sex AND getting paid - I can't say no to that, ever."
            money += Random(100, 300)
            perversion += Random(0, 0.25)
        Elseif 1
            Player(Sad):: "I'm sorry, <Actor.Daddy_or_Mommy>. I can't do it tonight, but I miss you too and can't wait to see you soon."
        Else
            "I don't know it was the right decision, but I felt like I was now in a stage of my life to move on from this particular arrangement. It's time to stand on my own two feet ... or find another sugarparent."
            SugarParent.clearGlobal()
            timeout(500, sugarbabe)
        Endif    
    Else
        "To be honest, I'm not earning all that much and it's expensive to live here. After rent and basic expenses, I really don't have much left. Sometimes, looking at celebrities and executives, I wish I can afford some of same luxuries they get."
        "I recently found a website that specialised in sugarparent-sugarbabe arrangements, basically wealthy older men and women pay for companionship and sex. Should I sign myself up?"
        0:: perversion > 20:: "Yes"
        1:: "No"
        
        If 0
            "It's worth a try. It's not like they will force me to do anything just from signing up."
            If Random(0, 100) < attractiveness                
                Actor = generatePersonTemporary()
                While !Actor.isInterestedIn(Player)
                    Actor = generatePersonTemporary()
                Endwhile
                Random
                    Actor.blendPreset(fourties)
                    Actor.blendPreset(fifties)
                    Actor.blendpreset(sixties)
                endRandom
                Actor.randomizeFace()
                Actor.randomizeHairs()
                
                "Eventually, I got approached by a potential <Actor.sugardaddy_or_sugarmommy>, who invited me to a first date."
                secondScreen(Actor)
                Actor.dress()
                Actor.show(2)
                "Should I agree to go on a date with <Actor.name>?"
                0:: "Yes"
                1:: "No"
                
                If 0
                    "Why not? I'm sure <Actor.he_or_she> will be extra generous on the first date."
                    secondScreen()
                    Loc = findNearbyBuilding(restaurant)
                    moveTo(Loc)
                    Actor.show()
                    Actor.dress()
                    Player.dress()
                    "<Actor.name> took me to a really fancy restaurant and wined and dined me like money was no object. Afterwards, <Actor.he_or_she> suggested that we go to a suite <Actor.he_or_she> had booked at a 5-star hotel nearby."
                    0:: "Yes"
                    1:: "No"
                    
                    If 0
                        Loc = findNearbyBuilding(hotel)
                        moveTo(Loc)
                        setBackground(home)
                        Actor.makePermanent()
                        "Wow, this is a nice hotel suite! Must have cost a fortune. And I know very well what <Actor.name> is expecting from me for <Actor.his_or_her> generousity."
                        Sex(Actor, Player)
                        perversion += Random(0, 0.5)
                        Actor.show(2)
                        "The sex wasn't too bad. We spent the night together. The morning after, <Actor.name> paid for breakfast, and before heading off to work, told me that <Actor.he_or_she> will call me soon and left me a generous envelope. Needless to say, <Actor.name> is now my <Actor.sugardaddy_or_sugarmommy>."
                        money += Random(100, 500)
                        SugarParent = Actor.getID()
                        SugarParent.setGlobal(SugarParent)
                    Else
                        "I turned down <Actor.name>'s offer to go back to the hotel. <Actor.He_or_She> was obviously disappointed and needless to say, never got in contact with me again."
                        timeout(500, sugarbabe)
                    Endif
                Else
                    "Ew, even for money, I can't sleep with someone that old and ugly."
                    timeout(500, sugarbabe)
                Endif
            Else
                "Unfortunately, I received no interest from any potential sugarparents. It seemed there simply was just too much competition and I was not ... attractive enough."
                timeout(500, sugarbabe)
            Endif
        Else
            "No way. There are more self-respecting ways to make money than that. It's basically glorified prostitution."
            timeout(1000, sugarbabe)
        Endif
    Endif
    
    SceneEnd()
    timeout(96, sugarbabe)