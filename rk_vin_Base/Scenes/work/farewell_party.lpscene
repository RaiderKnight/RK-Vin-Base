WHAT: work
WHERE: work
WHEN: 8 - 20
WHO: Colleague = getSpecific(Colleague); If Colleague:intelligence > Random(50, 100)
OTHER: Random(0, 100) < 5

SceneStart()
setBackground(work)

Quit = true
Colleague.dress()
Colleague.show(2)

If Colleague:rapportwithplayer > 0 && Colleague.isContactExchanged() 
    Colleague(Sad):: "<Player.name>, can we have a private chat?"
    Player(Happy):: "Yes, what's the matter, <Colleague.name>?"
    Colleague(Sad):: "I haven't told anyone else about this ... but I received a job offer from a rival company. You know I like the people here, but the offer is too lucrative to turn down."
    0:: "Congratulate <Colleague.him_or_her>"
    1:: "Convince <Colleague.him_or_her> to stay"
    
    If 0
        Player(Happy):: "That's great news! I'm so happy for you. By all means, go for it! You've got to do what's best for your career."
        Colleague(Happy):: "I'm glad that you understand. Thank you. We'll keep in touch even after I leave the company, okay?"
    Else
        Player(Happy):: "<Colleague.name>, I understand the appeal but I really hope that you stay with us. Who knows whether you will suit the new environment or not? Plus I'm sure the bosses here will recognize your performance soon and give you a promotion that is equally lucrative."
        If interpersonal > Random(0, 100)
            Quit = false
            Colleague(Surprised):: "I guess you're right. There are plenty of risks moving jobs and I already have it good here. I'll talk to my family about it and come up with a final decision"
            "Eventually, <Colleague.name> decided to stay with the company. The knowledge of the rival offer was never shared with anyone else but me."
        Else
            Colleague(Sad):: "I'm sorry but I don't think it would be a financially responsible decision to turn down such an offer. I have commitments to meet."
        Endif
    Endif
Endif

InvitedHome = false
If Quit
    Colleague.removeColleague()
    "Today, I received an e-mail from the boss: <Colleague.name> is leaving for another job elsewhere! And <Colleague.his_or_her> farewell party will be organized in a few days."
    "A few days later ..."
    setBackground(bar)
    If Colleague:rapportwithplayer < 0
        "I attended the farewell party out of politeness. The truth was: I didn't like the <Colleague.guy_or_girl> that much, so good riddance really ..."
    Else
        "Of course I had to attend <Colleague.name>'s farewell party. <Colleague.He_or_She> is one of the most likable <Colleague.guys_or_girls> in the office. I'm feeling quite sad and downing a decent amount of booze to quell my sadness."
        If Player.isInterestedIn(Colleague)
            "But is the sadness only the result of friendship with a colleague?"
            0:: "<Colleague.name> is just a friend"
            1:: "I have a crush on <Colleague.him_or_her>"
            
            If 0
                "The sadness is normal when your friend is leaving the company. Oh well, I'm sure we'll still hang out outside of work."
            Else
                "The truth is: I'm so sad to be losing <Colleague.name> because I have quite a crush on <Colleague.him_or_her>. Now that we will no longer be colleagues, maybe this is the time to tipsily admit my feelings for <Colleague.him_or_her>."
                0:: "Admit your feelings"
                1:: "Maybe not"
                
                If 0
                    "Today is the day: I finally have the courage to admit my feelings for <Colleague.name>."
                    If Colleague:attractiontoplayer > Random(0, 100)
                        Colleague(Flirty):: "I like you too, <Player.name>. Maybe now that we won't be colleagues anymore, we can be more than friends ..."
                        Colleague:attractiontoplayer += Random(0, 10)
                        0:: "Invite <Colleague.name> home"
                        1:: "Another day"
                        
                        If 0
                            "I need to have <Colleague.him_or_her> tonight. We agreed to come back to my place, but after the party concluded so that I wouldn't become the target of gossips tomorrow."
                            InvitedHome = true
                        Else
                            "We agreed to go on dates soon. Now that we are no longer colleagues, it's no longer unprofessional to do so."
                        Endif                            
                    Else
                        Colleague(Sad):: "I'm sorry but I only see you as a colleague and a friend."
                        "I was heartbroken but what could I do? I spent the rest of night drinking down my sorrows."
                    Endif
                Else
                    "I'd better not play out some drama in front of all my colleagues."
                Endif
            Endif
        Endif
    Endif
    "Eventually, the farewell party concluded. Everyone said their goodbye to <Colleague.name> and wished <Colleague.him_or_her> luck."
Endif

If InvitedHome
    "Little did the rest of my colleagues know, soon after they left, <Colleague.name> and I walked back to my place."
    moveTo(Home)
    "As soon as we were inside, we started kissing and undressing each other."
    Sex(Colleague, Player)
    Colleague.show(2)
    "After we had sex, we spent the night together. I might have lost <Colleague.name> as a colleague and won't get to see <Colleague.him_or_her> in the office everyday anymore, but we are going to be so much more than colleagues now!"
Endif

SceneEnd()
timeout(500, farewell_party)