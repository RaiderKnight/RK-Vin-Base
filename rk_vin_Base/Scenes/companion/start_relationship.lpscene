WHAT: all, -go_to_the_bathroom
WHERE: all, -toilets, -PC, -office
WHEN: 0-24
WHO: Companion = getCompanion(); If !CurrentCompanion.isRelative() && !CurrentCompanion.isDating() && !CurrentCompanion.hasRelationship(Spouses, Dating) && CurrentCompanion.IsInterestedIn(Player) && Player.IsInterestedIn(CurrentCompanion) && CurrentCompanion:attractiontoplayer > Random(30, 60)
OTHER: !Player.isDating() || [isModEnabled(vin_Polyamorous) && OpenRelationship()]

    SceneStart()
    CurrentCompanion.show(2)
    
    If CurrentCompanion.isCreature()
        "I can't propose a romantic relationship to an animal!"
    Else
        passTime(0.2, 0.5)

        If Player.IsDominantSex(CurrentCompanion) || [interpersonal > Random(70, 100) && masochist < -30] || !CurrentCompanion.isInterestedIn(Player)
            "I have spent a decent amount of time to get to know <CurrentCompanion.name>. However, have I ever imagined <CurrentCompanion.him_or_her> as my <CurrentCompanion.boyfriend_or_girlfriend>?"
            0:: "Yes"
            1:: "No"
            
            If 0
                "I have grown quite fond of <CurrentCompanion.him_or_her> indeed. But is this the right time when I should have the courage to ask <CurrentCompanion.him_or_her> to be my <CurrentCompanion.boyfriend_or_girlfriend>?"
                0:: "Yes"
                1:: "No"
                
                If 0
                    Player(Anxious):: "Listen, can we talk privately for a bit?"
                    CurrentCompanion(Curious):: "Of course, <Player.name>. What is it?"
                    
                    If interpersonal < 50
                        "Alright, this is the moment. <Player.name>, you can do this! Be brave. Keep calm ... Keep calm ... Don't screw this up!"
                    Endif
                    
                    Player(Happy):: "You know, although we haven't known each other for too long, I have grown quite fond of you and I hope you feel the same ..."
                    Player(Happy):: "If so, I would love nothing more than to take our relationship to the next level and be able to call you my <CurrentCompanion.boyfriend_or_girlfriend>."
                    CurrentCompanion(Surprised):: "<Player.name>! I ..."
                    
                    If CurrentCompanion:attractiontoplayer > Random(20, 60)
                        karma += 10
                        CurrentCompanion(Happy):: "I like you a lot too, <Player.name>. Of course I'll be your <CurrentCompanion.boyfriend_or_girlfriend>!"
                        CurrentCompanion.setDating()
                        mood += 100
                        
                        "So we embraced and kissed, treasuring our first moment as a couple."
                        0:: [IsModEnabled(vin_VanillaPorn) && perversion > Random(0, 50)]|| [IsModEnabled(vin_VanillaRealistic) && WHERE == home]:: "Celebrate with sex, right here"
                        1:: "Continue our date"
                        
                        If 0
                            Player(Wink):: "Baby, I just thought of the perfect way to celebrate the start of our romance!"
                            CurrentCompanion(Flirty):: "I think I know exactly what you mean!"
                            Sex(Player, CurrentCompanion)
                            perversion += Random(0, 0.5)
                            CurrentCompanion.show(2)
                            "After having sex, we continued our date, now as a new couple."
                        Else
                            "We continued our date, now as a new couple."
                        Endif

                    Else
                        CurrentCompanion(Surprised):: "<Player.name> ... I like you too ... but just not that way. I only see you as a friend ..."
                        "And ... I've been rejected ... My heart is broken and I don't know how to feel about this ... <CurrentCompanion.name> doesn't seem to want to lose me as a friend, but doesn't want our relationship to go any further than that."
                        mood -= 100
                        0:: "Accept the friend zone"
                        1:: "Stop seeing <CurrentCompanion.name> altogether"
                        
                        If 0
                            Player(Sad):: "Alright, I respect your decision. Let's just be friends ..."
                            CurrentCompanion:attractiontoplayer -= Random(0, 20)
                            CurrentCompanion:rapportwithplayer += Random(0, 20)
                            "Damn! Those words were painful to say, but as long as I manage to keep <CurrentCompanion.name> in my life, there's hope that one day <CurrentCompanion.he_or_she> will change <CurrentCompanion.her_or_his> mind."
                            "With that awkward conversation out of the way, we changed the subject and continued hanging out, although my mind was no longer into it."
                        Else
                            Player(Offended):: "No, if I cannot have your heart, then I'd rather not have you in my life at all!"
                            "Having said that, I stormed off, promising myself that was the last I'd seen of <CurrentCompanion.name>?"
                            CurrentCompanion.hide()
                            setBackground(home)
                            "A few hours later ..."
                            passTime(1, 2)
                            "My phone rang, it was <CurrentCompanion.name>. Still heartbroken, I pressed the 'Block' button. And just like that, <CurrentCompanion.he_or_she> was gone from my life. I convinced myself that it was the best for both for us."
                            blockContact(CurrentCompanion)
                            endDate()                 
                        Endif
                    Endif
                Else
                    "No, it's still too early. I want to get to know <CurrentCompanion.name> a bit more at least ..."
                Endif
            Else
                "Nope, I only see <CurrentCompanion.name> as a friend."
            Endif
        Else
            CurrentCompanion(Anxious):: "Listen, can we talk privately for a bit?"
            "<CurrentCompanion.name> has been acting rather weirdly today, as if he had something important to say to me all day."
            Player(Curious):: "Of course, <CurrentCompanion.name>. What is it?"
            
            If CurrentCompanion:interpersonal > Random(0, 30)
                CurrentCompanion(Happy):: "You know, although we haven't known each other for too long, I have grown quite fond of you and I hope you feel the same ..."
                CurrentCompanion(Happy):: "If so, I would love nothing more than to take our relationship to the next level and be able to call you my <Player.boyfriend_or_girlfriend>."
                0:: "Agree to be <CurrentCompanion.his_or_her> <Player.boyfriend_or_girlfriend>"
                1:: "Reject <CurrentCompanion.him_or_her>"
                
                If 0
                    karma += 10
                    Player(Happy):: "I like you a lot too, <CurrentCompanion.name>. Of course I'll be your <Player.boyfriend_or_girlfriend>!"
                    CurrentCompanion.setDating()
                    mood += 100
                    "So we embraced and kissed, treasuring our first moment as a couple."
                    0:: [IsModEnabled(vin_VanillaPorn) && perversion > Random(0, 50)]|| [IsModEnabled(vin_VanillaRealistic) && WHERE == home]:: "Celebrate with sex, right here"
                    1:: "Continue our date"
                    
                    If 0
                        Player(Wink):: "Baby, I just thought of the perfect way to celebrate the start of our romance!"
                        CurrentCompanion(Flirty):: "I think I know exactly what you mean!"
                        Sex(CurrentCompanion, Player)
                        perversion += Random(0, 0.5)
                        CurrentCompanion.show(2)
                        "After having sex, we continued our date, now as a new couple."
                    Else
                        "We continued our date, now as a new couple."
                    Endif
                Else
                    Player(Surprised):: "<CurrentCompanion.name> ... I like you too ... but just not that way. I only see you as a friend ..."
                    mood -= 30
                    "It was a difficult conversation to have. On one hand, I don't have any romantic interest in <CurrentCompanion.name>. On another hand, I don't want to lose <CurrentCompanion.him_or_her> as a friend." 
                    "Eventually ..."
                    If CurrentCompanion:rapportwithplayer + interpersonal > Random(0, 50)
                        CurrentCompanion(Sad):: "Alright, I respect your decision. Let's just be friends ..."
                        CurrentCompanion:attractiontoplayer -= Random(0, 20)
                        "Those words were clearly said with a lot of reluctance, but at least I managed to keep <CurrentCompanion.name> in my life ... for now."
                    Else
                        CurrentCompanion(Offended):: "No, if I cannot have your heart, then I'd rather not have you in my life at all!"
                        "Having said that, <CurrentCompanion.name> stormed off. Was that the last I've seen of <CurrentCompanion.him_or_her>?"
                        CurrentCompanion.hide()
                        setBackground(home)
                        "A few hours later ..."
                        passTime(1, 2)
                        "Thinking that <CurrentCompanion.he_or_she> would have calmed down by now, I tried to call, only to find out that I had been blocked! It's a sad day, I might have forever lost <CurrentCompanion.name> from my life ..."
                        blockContact(CurrentCompanion)
                        endDate()                
                    Endif
                Endif
            Else
                CurrentCompanion(Scared):: "I ... I ... Actually, can you believe it? I completely forgot what I wanted to say. Nevermind me!"
                "And so <CurrentCompanion.name> went back to acting weird for the rest of the day ..."
            Endif

        Endif
    Endif

    SceneEnd()
    timeout(96, start_relationship)