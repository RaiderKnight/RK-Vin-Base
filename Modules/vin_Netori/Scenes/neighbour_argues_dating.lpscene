WHAT: all, -sleep, -nap
WHERE: home
WHEN: 0 - 24 
WHO: Actor = getRelatedPeople(Spouses, Dating); If Actor0.isNeighbour() && !Actor1.isRelative() && Actor1.isInterestedIn(Player) && Random(0, 100)*Random(0, 1) > Actor0:interpersonal && Actor1:rapportwithplayer > 0
OTHER: !isWithCompanion()

    SceneStart()
    
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        UrgeToPeeEnable = rk_VB_UrgeToPeeEnable.getGlobal()
        UrgeToPee = Player.getActorVar(rk_VB_UrgeToPee)
        Actor0.dress()
        Actor1.dress()
        Actor0.show(2)
        Actor1.show(3)
        Actor0(Furious):: "..."
        Actor1(Furious):: "..."
        "My neighbour is having a fight with <Actor0.his_or_her> <Actor1.boyfriend_or_girlfriend> again ... At this rate, they won't last."
        Actor0(Furious):: "Fine, I'm leaving!"
        Actor0.hide()
        "And just like that, my neighbour stormed off in anger, leaving <Actor1.name> behind bawling <Actor1.his_or_her> eyes out."
        0:: "Console <Actor1.name>"
        1:: "Say nothing"
        
        If 0
            "I felt sorry for the poor <Actor1.guy_or_girl>. My neighbour was probably in the wrong here."
            "After some soothing words, I managed to stop <Actor1.name>'s tears, but <Actor1.he_or_she> is now leaning on my shoulder - which isn't the sight I would want my neighbour to come back to ..."
            Actor1(Sad):: "You would be the perfect <Player.boyfriend_or_girlfriend>."
            AnimatePair(Player, Actor1, Kissing)
            Actor1(Kiss):: "..."
            "Then all of a sudden, <Actor1.name> kissed me. Things are getting very heated ..."
            0:: "Let it happen"
            1:: "Put an end to this"
            
            If 0
                Actor1:attractiontoplayer += Random(0, 10)
                "Looks like <Actor1.name> wants some sweet revenge by sleeping with one of <Actor1.his_or_her> <Actor0.boyfriend_or_girlfriend>'s neighbours, and I cannot help but be complicit. My neighbour only has <Actor0.himself_or_herself> to blame."
                Sex(Player, Actor1)
                If HungerEnable > 0
                    Player.modifyActorVar(rk_VB_Hunger, -2)
                EndIf
                perversion += Random(0, 1)
                masochist -= Random(0, 5)
            Else
                "This has gone way too far. My neighbour would kill me if <Actor0.he_or_she> comes back to find us like this."
            Endif
        Else
            "It's none of my business. All couples have arguments from time to time."
        Endif
    SceneEnd()
    timeout(500, neighbour_argues_dating)