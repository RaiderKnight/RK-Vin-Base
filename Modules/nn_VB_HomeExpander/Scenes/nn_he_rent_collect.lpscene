WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    
    clearGetList()  // sometimes the getGlobal/getSpecific calls do not work without this call
    
    // Calculate the time in days since the last check
    // We use the players age for that
    // It is incremented by 1/365 every day
    daysSinceBirth = (Player:Age * 365)
    daysSinceBirth += 0.5
    daysSinceBirth = daysSinceBirth.round()
    daysSinceBirth -= 1
    
    lastCheckDays = RE_last_rent_collect_date.getGlobal()
    deltaLastCheck = daysSinceBirth - lastCheckDays
    
    weeksSinceLastCheck = 0.5 + deltaLastCheck / 7
    weeksSinceLastCheck = weeksSinceLastCheck.round()
    
    // "DEBUG: lastCheckDays=<lastCheckDays>, deltaLastCheck=<deltaLastCheck>, weeksSinceLastCheck=<weeksSinceLastCheck>"
    
    // Get Tenants
    secTenant = RE_second_Tenant.getGlobal()
    thirdTenant = RE_third_Tenant.getGlobal()
    TenantA = getSpecific(secTenant)
    TenantB = getSpecific(thirdTenant)
    
    // Hide tenants
    If TenantA.isValid()
        TenantA.hide()
    EndIf
    If TenantB.isValid()
        TenantB.hide()
    EndIf
    
    If TenantA.isValid() || TenantB.isValid()
        
        If !isAtHome() && PlayerHasHome()
            moveTo(home)
        EndIf 
        
        // Initialize the rent check...
        If lastCheckDays == 0
            deltaLastCheck = -1
        EndIf
        
        If deltaLastCheck < 7
            If deltaLastCheck < 0
                RE_last_rent_collect_date.setGlobal(daysSinceBirth)
                "You will have to wait 7 days until you can collect rent."
            Else
                wait = 7 - deltaLastCheck
                wait += 0.5
                wait = wait.round()
                wait -= 1
                "You will have to wait <wait> days until you can collect rent again."
            EndIf
        Else
            // Remember the current date as last check time
            RE_last_rent_collect_date.setGlobal(daysSinceBirth)
            
            retalFee = 0
            If TenantA.isValid()
                movedInDate = RE_second_Tenant_rent_date.getGlobal()
                rentA = RE_second_Tenant_rent.getGlobal()
                retalFee += rentA
            EndIf
            
            If TenantB.isValid()
                movedInDate = RE_third_Tenant_rent_date.getGlobal()
                rentB = RE_third_Tenant_rent.getGlobal()
                retalFee += rentB
            EndIf
            
            retalFee *= weeksSinceLastCheck
            rentalFeeLocal = retalFee.convertToLocalCurrency()
            "You earned <rentalFeeLocal> for <weeksSinceLastCheck> weeks!"
            
            money += retalFee
        EndIf
    Else
        "Well, seems you first need to rent one of your bedrooms to be able to collect rent."
    EndIf
    
    SceneEnd()