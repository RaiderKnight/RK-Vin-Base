WHAT: all, -sleep
WHERE: all
WHEN: 6 - 22
WHO: none
OTHER: none

    hasRentAdvertisements = RE_has_advertisements.getGlobal()
    If hasRentAdvertisements>0
        secTenantID = RE_second_Tenant.getGlobal()
        thirdTenantID = RE_third_Tenant.getGlobal()
        TenantA = getSpecific(secTenantID)
        TenantB = getSpecific(thirdTenantID)
        
        // Calculate the current time in days
        // We use the players age for that
        // It is incremented by 1/365 every day
        daysSinceBirth = (Player:Age * 365)
        daysSinceBirth += 0.5
        daysSinceBirth = daysSinceBirth.round()
        daysSinceBirth -= 1
        
        SceneStart()
        "Your phone is ringing..."
        Player.animate(call)
        
        Actor = generatePersonTemporary()
        Actor.dress()
        secondScreen(Actor)
        Actor.show()
        
        Random
            Actor(Serene)::"Hey <Player.Mr_or_Mrs> <Player.name_last>! I saw your ad about the room. Is it still available?"
            Actor(Serene)::"Hello, my name is <Actor.Name> <Actor.Name_last>. I'm calling about the room that is in the advertisements. Is it still available?"
            Actor(Serene)::"Hello <Player.Mr_or_Mrs> <Player.name_last>! This is <Actor.Name> <Actor.Name_last> speaking. I would like to know if the room that you advertised is still available?"
        EndRandom
        
        canRent = 1
        If TenantA.isValid() && TenantB.isValid()
            canRent = 0
        EndIf
        1::canRent>0::"Yeah, it's still available."
        0::"Oh sorry! I just let it to someone."
        
        If 0
            Random
                Actor(Sad)::"Oh bad luck. Thanks anyway!"
                Actor(Sad)::"Oh damn! I really need one... Alright thanks!"
                Actor(Sad)::"Shit! Again I come too late... *beep*"
                Actor(Sad)::"Oh no. I'm really out ouf luck! Thanks <Player.Mr_or_Mrs> <Player.name_last>!"
            EndRandom
        Else
            Random
                Actor(Happy)::"Oh that's great. How much is the rent?"
                Actor(Happy)::"Awesome. This is the first time I don't get a 'NO'. How much is the rent?"
                Actor(Happy)::"This must be my lucky day! I really need a room in your area. How much will it be?"
                Actor(Happy)::"Oh this is great <Player.Mr_or_Mrs> <Player.name_last>! How much will the rental fee be?"
            EndRandom
            
            // negotiate on the price
            // <-- begin copy from nn_he_rent_room.lpscene
            rentA = 200
            rentB = 150
            rentC = 100
            rentD = 75
            rentE = 50
            rentF = 25
            rentNull = 0
            
            rentALocal = rentA.convertToLocalCurrency()
            rentBLocal = rentB.convertToLocalCurrency()
            rentCLocal = rentC.convertToLocalCurrency()
            rentDLocal = rentD.convertToLocalCurrency()
            rentELocal = rentE.convertToLocalCurrency()
            rentFLocal = rentF.convertToLocalCurrency()
            rentNullLocal = rentNull.convertToLocalCurrency()
            
            //"How much is the rental fee per week?"
            0::"It's free (<rentNullLocal>)"
            1::"Ask for <rentALocal>"
            2::"Ask for <rentBLocal>"
            3::"Ask for <rentCLocal>"
            4::"Ask for <rentDLocal>"
            5::"Ask for <rentELocal>"
            6::"Ask for <rentFLocal>"
            
            reqRent = 0
            If 1
                reqRent = rentA
            ElseIf 2
                reqRent = rentB
            ElseIf 3
                reqRent = rentC
            ElseIf 4
                reqRent = rentD
            ElseIf 5
                reqRent = rentE
            ElseIf 6
                reqRent = rentF
            EndIf
            
            rent = reqRent
            rentLocal = reqRent.convertToLocalCurrency()
            // --> end copy
            
            Player(Serene)::"It will be <rentLocal> per week <Actor.Name>."
            chance = Random(0,10)
            willMoveIn = true
            If chance <= 5
                Random
                    Actor(Serene)::"Alright <Player.Mr_or_Mrs> <Player.name_last>, this sounds fair. I'll pass by in a few hours. See you then."
                    Actor(Serene)::"Okay, this is a good price. I'll take it <Player.Mr_or_Mrs> <Player.name_last>! See you in a few minutes!"
                    Actor(Attentive)::"That's awesome. I'll be at your location in a few minutes to sign the contract."
                    Actor(Happy)::"Wow <Player.Name>, what a great offer! Alright I'll move in this week!"
                EndRandom
            Else
                // <-- begin copy from nn_he_rent_room.lpscene
                Random
                    Actor(Sad)::"Oh <Player.Mr_or_Mrs> <Player.name_last> I'd love to, but this is way over my budget!"
                    Actor(Sad)::"Oh <Player.Mr_or_Mrs> <Player.name_last> I cannot afford this."
                    Actor(Sad)::"I am afraid I cannot pay that much <Player.Mr_or_Mrs> <Player.name_last>."
                EndRandom
                
                negotiate = 1
                While negotiate > 0
                    "<Actor.Name> cannot afford a rent that high. Offer a discount?"
                    0::"Ok, <Actor.Name> can stay with me for free (<rentNullLocal>)"
                    1::"Offer 10% discount"
                    2::"Offer 25% discount"
                    3::"Offer 35% discount"
                    4::"Offer 50% discount"
                    5::"Offer 75% discount"
                    99::"Bad luck! (do not rent to <Actor.Name>)"
                    
                    If 0
                        Random
                            Player(Wink)::"Alright <Actor.Name>, here comes the deal: You live with me for free and just take over some housework. OK?"
                            Player(Happy)::"Hm <Actor.Name>, OK. I see your situation is not easy at the moment. You can stay with me for free."
                        EndRandom
                        
                        chance = Random(0,10)
                        If chance <2 
                            // dumb ass...
                            willMoveIn = false
                            Random
                                Actor(confused)::"Eh? What a cheesy offer. No thanks <Player.Mr_or_Mrs> <Player.name_last>!"
                                Actor(irritated)::"Erm, <Player.Mr_or_Mrs> <Player.name_last> - thanks but I just decided I don't want to live with you. Thanks anyway!"
                                Actor(Anxious)::"Oh thanks for your kindness <Player.Mr_or_Mrs> <Player.name_last>. But I think I have to decline that offer. Bye!"
                            EndRandom
                        EndIf
                        negotiate = 0
                    ElseIf 1||2||3||4||5
                        discount = 0.9
                        If choice == 2
                            discount = 0.75
                        ElseIf choice == 3
                            discount = 0.65
                        ElseIf choice == 4
                            discount = 0.5
                        ElseIf choice == 5
                            discount = 0.25
                        EndIf
                        reqRent = rent * discount
                        rent = reqRent
                        rentLocal = reqRent.convertToLocalCurrency()
                        
                        Random
                            Player(Wink)::"Alright <Actor.Name>, here comes the deal: You live with me for just <rentLocal> and take over some housework. OK?"
                            Player(Happy)::"Hm <Actor.Name>, OK. I see your situation is not easy at the moment. You can stay with me for <rentLocal> per week. Deal?"
                        EndRandom
                        
                        chance = Random(0,10)
                        If chance >=2
                            // OK
                            Random
                                Actor(Happy)::"Alright this is a pretty good deal <Player.Mr_or_Mrs> <Player.name_last>. Thank you very much!"
                                Actor(Happy)::"Ok, this fits my budget! Thanks a lot <Player.Mr_or_Mrs> <Player.name_last>!"
                                Actor(Happy)::"Oh thanks for your kindness <Player.Mr_or_Mrs> <Player.name_last>. I will help you with all the housework!"
                            EndRandom
                            needText = 0
                            negotiate = 0
                        Else
                            // dumb ass...
                            willMoveIn = false
                            negotiate = 0
                            
                            Random
                                Actor(confused)::"Eh? What a cheesy offer. No thanks <Player.Mr_or_Mrs> <Player.name_last>!"
                                Actor(irritated)::"Erm, <Player.Mr_or_Mrs> <Player.name_last> - thanks but I just decided I don't want to live with you. Thanks anyway!"
                                Actor(Anxious)::"Oh thanks for your kindness <Player.Mr_or_Mrs> <Player.name_last>. But I think I have to decline that offer. Bye!"
                            EndRandom
                        EndIf
                    ElseIf 99
                        Random
                            Player(Sad)::"Sorry <Actor.Name>, I cannot go any lower. Seems we won't find a solution now."
                            Player(Sad)::"Sorry <Actor.Name>, that is as low as I can go. Sorry."
                        EndRandom
                        
                        willMoveIn = false
                        needText = 0
                        negotiate = 0
                    EndIf
                EndWhile
                // --> end copy
            EndIf
            
            If willMoveIn
                Random
                    Player(Happy)::"Alright <Actor.Name>, see you soon and please call me <Player.Name>, OK?"
                    Player(Happy)::"Great! See you soon <Actor.Name>, and please call me <Player.Name>, right?"
                    Player(Happy)::"Awesome! See you <Actor.Name>, and please just call me <Player.Name>."
                    Player(Happy)::"Cool. So see you later <Actor.Name>! And: just call me <Player.Name> please!"
                    Player(Happy)::"Okay <Actor.Name> - lets meet at the location. And by the way: just call me <Player.Name>!"
                EndRandom
                
                Actor.makePermanent()
                exchangeContact(Actor)
                actId = Actor.getId()
                If !TenantA.isValid()
                    RE_second_Tenant.setGlobal(actId)
                    RE_second_Tenant_rent_date.setGlobal(daysSinceBirth)
                    RE_second_Tenant_rent.setGlobal(rent)
                    "You assigned bedroom 2 to <Actor.Name>. <Actor.S> was added to your contacts."
                    TenantA = Actor
                ElseIf !TenantB.isValid()
                    RE_third_Tenant.setGlobal(actId)
                    RE_third_Tenant_rent_date.setGlobal(daysSinceBirth)
                    RE_third_Tenant_rent.setGlobal(rent)
                    "You assigned bedroom 3 to <Actor.Name>. <Actor.S> was added to your contacts."
                    TenantB = Actor
                Else
                    "OOPS... Something is wrong. I don't know which room to assign!"
                EndIf
                
                If TenantA.isValid() && TenantB.isValid()
                    RE_has_advertisements.setGlobal(0)
                    "As both rooms are now occupied you stop advertising the rooms."
                EndIf
            EndIf
        EndIf
        
        Actor.hide()
        secondScreen()
        Player.animate()
        
        SceneEnd()
    EndIf
    
    timeout(2, nn_he_random_room_inquiry) // cooldown on the scene appearing again