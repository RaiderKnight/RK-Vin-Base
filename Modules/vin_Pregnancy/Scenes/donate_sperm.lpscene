WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    If Player.isFemale()
        "I don't have the tools to produce sperm ..."
    Elseif fertility == 0
        "I'm not fertile at all so my sperm is useless ..."
    Elseif isTimingOut(donate_sperm)
        "It's too soon since the last time I donated sperm. It takes at least 24 hours for the sperm count to reach the required level."
    Else
        Price = 0.2*[fitness + attractiveness + intelligence]*CostOfLiving
        PriceC = Price.convertToLocalCurrency(false)
        "Based on my level of fitness, attractiveness and intelligence, the current rate I would be paid for each sperm donation is <PriceC>."
        0:: "Donate sperm"
        1:: "Not today"
        
        If 0
            timeoutPrecise(24, donate_sperm)
            Actor = generatePersonTemporary()
            If Random(30, 100) < Actor:perversion && Actor.isInterestedIn(Player)
                Actor.dress()
                Actor.show()
                "The nurse in charge of collecting my sperm today is <Actor.name> ... who is offering to literally give me a hand so that the samples can be collected quicker."
                0:: "Receive a handjob"
                1:: "I'll do it myself"
                
                If 0
                    Masturbate = false
                    Filter(Handjob)
                    Sex(Player, Actor)
                    perversion += 0.5
                Else
                    Masturbate = true
                Endif
            Else
                Masturbate = true
            Endif
            If Masturbate
                "Okay, let's get on with it. Jerking off to cum into a vial while a nurse watches is harder than you think ..."
                Filter(Solo)
                Sex(Player)
            Endif
            money => money + Price
            If Random(100, 600) < fitness + attractiveness + intelligence
                If Random(100, 600) < fitness + attractiveness + intelligence && !isTimingOut(invited_impreg_party)
                    followUp(invited_impreg_party)
                Else
                    Actor2 = generatePersonTemporary()
                    While Actor2.isMale()
                        Actor2 = generatePersonTemporary()
                    Endwhile
                    Actor2.dress()
                    Actor2.show()
                    "As soon as I left the sperm bank clinic, I was approached by a strange woman who also just left the clinic. She said the artificial insemination procedure scares her and would prefer to get pregnant the traditional way."
                    "And since I already donated my sperm to the sperm bank, it was possible that my sperm would be used to impregnate her anyway ... so why not follow her to a hotel now and do a much simpler procedure there for five times the money I've just received from the clinic?"
                    0:: "Okay"
                    1:: "No"
                    
                    If 0
                        Actor2.makePermanent()
                        Actor2.setWantsBabies()
                        setBackground(hotel)
                        "Soon enough I was back in a hotel with her ..."
                        Sex(Player, Actor2)
                        Actor2.impregnate(Player)
                        money => money + Price*5
                    Endif
                Endif
            Endif
        Endif
    Endif
    SceneEnd()