WHAT: none
WHERE: none
WHEN: 0-24
WHO: none
OTHER: none


    SceneStart()
        CompanionIntroduced = isWithCompanion()
        New_Person.makePermanent()
        InterestedInPC = New_Person.isInterestedIn(Player)
        DominantToPC = New_Person.isDominantSex(Player)
        If WHERE == work
            New_Person.addColleague()
        Elseif WHERE == home
            If isAtHome()
                New_Person.addNeighbour()
            Else
                New_Person.addDatingFriend()
            EndIf
        Elseif WHERE == university:work || WHERE == university:school
            New_Person.setDifferentMajor()
        EndIf
        // Start edit Raddeck
        Random
            SorryTXT = "I was wondering if..."
            SorryTXT = "Such a beautiful weather today..."
            SorryTXT = "Sorry to bother you but,..."
        EndRandom
        Player(Happy)::"Hi my name is <Player.name>, nice to see you! <SorryTXT>"
        Player.moveToPersonStand(New_Person, 50)
        // End edit Raddeck
        If InterestedInPC && [DominantToPC || Random(0,1) < 0.2] && Random(0, 20) < [New_Person:attractiontoplayer + New_Person:interpersonal]
            "I found myself engaged in a small conversation with someone interesting - <New_Person.He_or_She> says <New_Person.his_or_her> name is <New_Person.name>."
            "Before leaving <New_Person.he_or_she> asked for my number. Should I accept <New_Person.his_or_her> request?"
            0:: "Yes"
            1:: "No"
            
            If 0
                exchangeContact(New_Person)
                "I happily accepted. We exchanged numbers and went our separate ways."
                If CompanionIntroduced
                    CurrentCompanion(Happy):: "<New_Person.name> is a great person. You made the right choice giving <New_Person.him_or_her> your number."
                    CurrentCompanion:rapportwithplayer += Random(0,1)
                EndIf
                interpersonal += 1  // Edit Raddeck
            Else
                "I found an excuse not to give <New_Person.him_or_her> my number. Disappointed, <New_Person.he_or_she> left."
                If CompanionIntroduced
                    CurrentCompanion(Angry):: "You shouldn't have been so rude to my friend! Next time I won't introduce you to anyone else!"
                    CurrentCompanion:rapportwithplayer -= Random(0,2)
                EndIf
                interpersonal -= 1  // Edit Raddeck
    
            EndIf            
        Else
            Random
                "I came across someone interesting, who introduced <New_Person.himself_or_herself> as <New_Person.name>. Should I exchange contacts with <New_Person.him_or_her>?"
                "Just by chance, I met <New_Person.name>, who seemed okay. Perhaps I should ask for <New_Person.his_or_her> phone number?"
            EndRandom
            
            0:: interpersonal > 5:: "Yes"
            1:: "No"
                    
            If 0
                If [New_Person:attractiontoplayer < 20 && InterestedInPC] || [!InterestedInPC && New_Person:rapportwithplayer < 0]
                    Random
                        New_Person(Happy):: "Sorry, but I don't feel like I know you well enough to share such personal information just yet."
                        New_Person(Happy):: "It's gonna be a no, I'm afraid. Another day perhaps."
                        New_Person(Irritated):: "Sorry, but I don't normally exchange numbers with someone I've just met."
                    EndRandom      
                    If CompanionIntroduced && [CurrentCompanion:rapportwithplayer > 30 || CurrentCompanion:attractiontoplayer > 30]
                        CurrentCompanion(Irritated):: "Come on, <New_Person.name>. <Player.name> is a good person. You have nothing to worry about!"
                        New_Person(Happy):: "Alright then, if you insist. Sorry for being too cautious!"
                        exchangeContact(New_Person)
                        "We exchanged numbers and parted ways."
                    Else
                        0:: "I understand."
                        1:: masochist < 30:: "It's your loss!"

                        If 0
                            Random
                                Player(Happy):: "That's okay. I understand. You've got to be careful with private information these days."
                                Player(Happy):: "No worries. It's not a big deal."
                                Player(Wink):: "Did I scare you away? Haha! Don't worry. See you around, I guess."
                            EndRandom
                                    
                            New_Person:rapportwithplayer += Random(1,5)
                            If InterestedInPC
                                New_Person:attractiontoplayer -= ( Random(1,10)*New_Person:masochist/100 )
                            EndIf
                                    
                        Else
                            Random
                                Player(Angry):: "Fine then. It's your loss being so precious!"
                                Player(Angry):: "Get out of my sight then!"
                                Player(Sarcastic):: "Well, looks like someone has a overly high opinion of <New_Person.himself_or_herself> ..."
                            EndRandom
                                    
                            New_Person:rapportwithplayer -= Random(1,10)
                            If InterestedInPC
                                New_Person:attractiontoplayer += ( Random(1,20)*New_Person:masochist/100 )
                                If New_Person:attractiontoplayer > 50
                                    Random
                                        New_Person(Scared):: "Well ... I .... I'm sorry if I came across as arrogant or something. Let's exchange numbers!"
                                        New_Person(Scared):: "Sorry. I didn't mean any offence ... On second thought, here, you can have my number."
                                        New_Person(Embarassed):: "Sorry. I had a bad day ... On second thought, here, you can have my number." // Edit Raddeck
                                    EndRandom
                                    exchangeContact(New_Person)
                                EndIf
                            EndIf
                        EndIf
                    EndIf
                Else
                    "<New_Person.name> happily accepted. We exchanged numbers and went our separate ways." // Edit Raddeck
                    exchangeContact(New_Person)
                    New_Person:rapportwithplayer += Random(0,1) // Edit Raddeck
                EndIf
            Else
                "I decided not to give <New_Person.him_or_her> my number."
                interpersonal -= 1 // Edit Raddeck
            EndIf        
        EndIf
    SceneEnd() // Improved by Raddeck