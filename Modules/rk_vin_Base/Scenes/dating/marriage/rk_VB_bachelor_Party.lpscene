WHAT: none
WHERE: none
WHEN: 0-24
WHO: Companion = getCompanion(); If CurrentCompanion.isDating() && CurrentCompanion:attractiontoplayer > Random(65, 90) && CurrentCompanion:rapportwithplayer > Random(65, 90)
OTHER: !isPlayerMarried()

    SceneStart()

        Companion = getCompanion()
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        FacebookFriend = rk_VB_FacebookFriend.getGlobal()
        UrgeToPeeEnable = rk_VB_UrgeToPeeEnable.getGlobal()
        UrgeToPee = Player.getActorVar(rk_VB_UrgeToPee)
    
        Engaged = true
        NPCisEngaged = rk_VB_isEngaged.getGlobal()
    
        If Engaged || NPCisEngaged > 0
            Cheated = false
            CurrentCompanion.hide()
            setBackground(home)
            "With our wedding coming up, should I have a <Player.bachelor_or_bachelorette> party without my fiance?"
            0:: "Yes"
            1:: "No"
            
            If 0
                "Why not? I might not have many more chances to let loose after our wedding."
                "With that decision made, one of my best friends, a party animal, immediately volunteered to organize a <Player.bachelor_or_bachelorette> party for us. <Player.He_or_She> didn't tell me the details of what's going to happen on the day, but told me to expect some pleasant surprises."
                setBackground(nightclub)
                "Tonight is the night. My friend got us a nice limousine to take us to the most expensive nightclub in the city. We had so much booze on the limousine that by the time we arrived at the club, we were already quite drunk."
                "I can't believe it! My friend managed to book the nightclub's VIP area all for us. This must have cost a fortune! I knew I could rely on my friend ..."
                "After many more rounds of expensive alcohol, I was told to close my eyes for the surprise of the night that my friends had in store for me. Being quite drunk already, I happily obliged."
                Stripper = generatePersonTemporary()
                While !Player.isInterestedIn(Stripper)
                    Stripper = generatePersonTemporary()
                Endwhile
                Stripper2 = generatePersonTemporary()
                While !Player.isInterestedIn(Stripper2)
                    Stripper2 = generatePersonTemporary()
                Endwhile
                Stripper.blendPreset(twenties)
                Stripper2.blendPreset(twenties)
                
                If Stripper.isMale()
                    Stripper.blendPreset(bodybuilder)
                Else
                    Stripper.blendpreset(hourglass1_F)
                Endif
                Stripper.randomizeFace()
                Stripper.randomizeHairs()
                
                If Stripper2.isMale()
                    Stripper2.blendPreset(bodybuilder)
                Else
                    Stripper2.blendpreset(hourglass1_F)
                Endif
                Stripper2.randomizeFace()
                Stripper2.randomizeHairs()
                
                Stripper.show(2)
                Stripper2.show(3)
                "My friends then told me to open my eyes, and I couldn't believe what I was seeing ... Two hot strippers are dancing in front of me with all my friends cheering them on."
                "My best friend whispered into my ear, assuring me that the strippers have already been paid for some 'extra service'. <Player.He_or_She> suggested I have one last threesome with these hotties before being tied down to one <CurrentCompanion.man_or_woman> for the rest of my life."
                0:: "Yes"
                1:: "No"
                
                If 0
                    "Oh well, as long as <CurrentCompanion.name> doesn't find out, this would be an experience I'll never forget. People do this on <Player.bachelor_or_bachelorette> parties all the time, I'm sure ..."
                    karma -= 20
                    Sex(Stripper, Player, Stripper2)
                    Player.show(0)
                    If HungerEnable > 0
                        Player.modifyActorVar(rk_VB_Hunger, -3)
                    EndIf
                    Cheated = true
                Else
                    "No way, this is going way too far. I must remain faithful to my fiance and not give in to peer pressure. I can't risk our marriage just for a few minutes of wild fun."
                    "Having made my decision, I sent the strippers on their way. My friends were clearly disappointed and the whole thing was a bit of a killjoy for the rest of the party."
                Endif
                Stripper.hide()
                Stripper2.hide()
            Else
                CurrentCompanion:attractiontoplayer += Random(0, 5)
                "No, nothing good would come out of it. Let's just focus on making the proper preparations for the wedding."
            Endif
            
            Actor = getSpecific(ExDating)
            If Actor.isValid() && Actor.isContactExchanged() && Actor:masochist > 0 && Actor:rapportwithplayer > 0 && Actor:attractiontoplayer > 20
                setBackground(home)
                "I heard a knock on my door. I wonder who it might be."
                Actor.dress()
                Actor.show(2)
                "It's my ex <Actor.name>!"
                
                Actor(Happy):: "<Player.name>, I found out from Facebook ... you're getting married to <CurrentCompanion.name> ..."
                "As far as exes go, we're still on quite good terms, but I obviously didn't invite <Actor.name> to the wedding. Not exactly the easiest announcement to make to an ex."
                Actor(Sad):: "I guess it's your life and you have every right to make that choice ... I'm glad you found a <CurrentCompanion.man_or_woman> to settle down with ... I'm happy for you ..."
                "<Actor.name>'s facial expression certainly didn't scream out 'happy'. Poor <Actor.guy_or_girl>, maybe deep inside <Actor.name> still has feelings for me ... and to be honest, maybe I do feel the same."
                0:: "Just thank <Actor.him_or_her>"
                1:: "Invite <Actor.him_or_her> in"
    
                If 0
                    Player(Sad):: "Thank you ... You should go though. It would be troublesome if my fiance sees you here."
                    Actor:attractiontoplayer -= Random(0, 3)
                Else
                    Player(Sad):: "Thank you ... That's very kind of you."
                    "Feeling sorry for my ex, I decided to invite <Actor.him_or_her> inside to chat a bit. I told myself nothing would happen and I was only doing it out of sympathy."
                    Player(Sad):: "Actually, do you want to come inside for a bit? We're still friends right?"
                    choice = -1    
                    ate = false
                    hadSex = false
                        
                    While choice < 5
                        Kissed = false
                        "What should I suggest to <Actor.name> that we do?"
                        0:: "Chat"
                        1:: !ate && stock_snack > 0.8:: "Eat something together"
                        2:: stock_movie > 0.8 :: "Watch a movie"
                        3:: stock_alcohol >= 1.8 :: "Drink alcohol"
                        4:: !hadSex:: "Kiss <Actor.him_or_her>"
                        5:: MyHome:: "Politely ask <Actor.him_or_her> to leave"
                
                        If 5
                            "We said goodbye and went our ways."
                            If HungerEnable > 0
                                Player.modifyActorVar(rk_VB_Hunger, -1)
                            EndIf
                        Else
                            If 0
                        
                                "We simply sat and chat for a while ..."
                                mood += 1
                                Actor:attractiontoplayer += 1
                                Actor:rapportwithplayer += 1
                                passTime(0.1, 0.2)
                            
                            Elseif 1
                        
                                "We made some quick snacks and enjoyed them together ..."
                                ate = true
                                If HungerEnable > 0
                                    Player.modifyActorVar(rk_VB_Hunger, 5)
                                EndIf
                                Actor:attractiontoplayer += 1
                                Actor:rapportwithplayer += 1
                                mood += 1
                                stock_snack -= 1
                                energy += 5
                                passTime(0.2, 0.4)
                            
                            Elseif 2
                        
                                "We watched a movie together ..."
                                stock_movie -= 1
                                mood += 1
                                Actor:attractiontoplayer += 1
                                Actor:rapportwithplayer += 1
                                If HungerEnable > 0
                                    Player.modifyActorVar(rk_VB_Hunger, -0.5)
                                EndIf
                                energy -= 1
                                passTime(1, 2)
                            
                            Elseif 3
                        
                                "We had a drink together ..."
                            
                                Player.animate(drinkglass)
                                Actor.animate(drinkglass)
                                "We had a drink alcohol together ..."
                                Player:intoxication += Random(0,10)
                                Actor:intoxication += Random(0,10)
                                stock_alcohol -= 2
                                passTime(0.1, 0.2)
                                Player.animate()
                                Actor.animate()
                                If UrgeToPeeEnable > 0
                                    Player.modifyActorVar(rk_VB_UrgeToPee, 1)
                                EndIf
                            
                            Elseif 4
                                Kissed = true
                            Endif
    
                            If Kissed || [Actor.isInterestedIn(Player) && !hadSex && Actor:attractiontoplayer > Random(0, 200)]
                                AnimatePair(Player, Actor, Kissing)
                                Player(Kiss):: "..."
                                Actor(Kiss):: "..."
                                If Dating
                                    "We kissed. It started slow but soon became more passionate. It was obvious what we both wanted ..."
                                Else
                                    "We had a kiss, an awkward one at first, but soon moving towards a more passionate direction."
                                Endif
                                "Should I let this escalate further?"
                                0:: Vanilla:: "Make out"
                                1:: "Stop"
                        
                                If 0
                                
                                    If Actor.isDating() || [Actor.isInterestedIn(Player) && [Actor.IsDominantSex(Player) || Actor:attractiontoplayer + Actor:perversion > Random(50, 200) || Actor:intoxication > 80]]
                                    
                                        "Our kiss got more and more steamy and soon we were making out, our hands all over each other."
                                    
                                        If TfGame() && Actor.isMale()
                                            "I'm a man trapped inside a woman's body. I don't think I'm gay, but here I am, about to hook up with my male date."
                                        Endif
                                    
                                        hadSex = true
                                        Player:perversion += Random(0, 1)
                                        Sex(Player, Actor)
                                        Actor.show(2)
                                        "The sex was amazing. We put our clothes back on afterwards and continued our date."
                                        If HungerEnable > 0
                                            Player.modifyActorVar(rk_VB_Hunger, -2)
                                        EndIf
                                        Cheated = true
                                        passTime(0.2, 0.5)
                                    
                                    Else
                                
                                        "Unfortunately for me, <Actor.name> didn't seem interested in letting things escalate any further and pulled away."
                                    
                                    Endif
                                
                                Else
                            
                                    "I decided to stop it there and not go any further."
                                
                                Endif
                            
                            Elseif Random(0, 100) < 20
                                If Dating
                                    "After a while, we were both exhausted and went to bed together for the night."
                                    passTime(4, 8)
                                    energy += Random(50, 100)
                                    "The next morning ..."
                                    If MyHome
                                        Actor(Happy):: "I've gotta go now, honey. Thanks for letting me stay the night. I can't wait to see you again soon!"
                                    Else
                                        Player(Happy):: "I've gotta go now, honey. Thanks for letting me stay the night. I can't wait to see you again soon!"
                                    Endif
                                    Actor:attractiontoplayer += Random(0, 1)
                                Else
                                    If MyHome
                                        Actor(Happy):: "I've gotta go now. Thank you for inviting me in."
                                        Player(Happy):: "Alright. Thank you for staying. Goodbye!"
                                    Else
                                        Actor(Happy):: "It has been a great day, but I'm getting quite tired now. Do you mind ..."
                                        "Getting the message from <Actor.name>, I said goodbye."
                                    Endif
                                    "We parted ways."
                                Endif
                                choice = 5            
                            Endif        
                        Endif
                    Endwhile
            
                Endif
                Actor.hide()
            Endif
            
            Affair = getSpecific(Affair)
            If intelligence < CurrentCompanion:intelligence*Random(0, 1) && [Cheated || Affair.isValid()]
            
                setBackground(home)
                CurrentCompanion.show(2)
                CurrentCompanion(Angry):: "<Player.name>, we need to talk!"
                CurrentCompanion:attractiontoplayer -= Random(0, 50)
                "This can't be good ..."
                Player(Anxious):: "Yes, darling, what is it? You don't look too happy ..."
                CurrentCompanion(Angry):: "Of course I don't, and you know damn well why!"
                CurrentCompanion(Furious):: "I know about your little romp. You <Player.asshole_or_slut>, you were sleeping around behind my back! I trusted you. I loved you. I wanted to spend the rest of my life with you, yet you betrayed me!"
                "And just like that, <CurrentCompanion.name> called off the engagement and broke up with me ... not too long before our planned wedding date. I found out later that <CurrentCompanion.he_or_she> also blocked my number."
                LoseDating()
                mood -= 100
                blockContact(Dating)
                
            Else
            
                setBackground(work)
                Master = generatePersonTemporary()
                While !Master.isMale()
                    Master = generatePersonTemporary()
                Endwhile
                Master.dress()
                
                // Start edit Ravenger6660
                If !Player.isSameGender(CurrentCompanion) && !CurrentCompanion.isExRelative()
                    setBackground(place_of_worship)
                Else
                    setBackground(restaurant)
                Endif
                // End edit Ravenger6660
                
                setBackground(biergarten)
                "The most important day of my life is here ..."
                If CurrentCompanion.isExRelative()
                    If isQuestCompleted(legalized)
                        "Thanks to the recent legalization of incest, we had no problem registering the marriage with the government and have a proper public wedding. Incest being legal doesn't stop some close-minded people from talking shit about us of course, but we couldn't care less about that."
                    Else
                        "Registering this marriage with the government is a no-goer - incest is illegal in this country so that would just send us straight to jail ... Still, we wanted to have a proper wedding, so we only only invited a few particularly open-minded family and friends, as well as guests who met us after we became a couple and had no idea we were related."
                    Endif
                Endif
                
                Player.show(0)
                CurrentCompanion.show(1)
                Master.show(3)
                Player.dressWedding()
                CurrentCompanion.dressWedding()
                Master(Happy):: "Today, <Player.name> and <CurrentCompanion.name> honored you with their invitation to be present with them today for this ceremony."
                Master(Happy):: "And so, this day, they declare before all of us that they shall live together in marriage."
                Master(Happy):: "In the traditional way, they entered into their marriage with the joining of hands, the making of vows and promises, and the giving and receiving of rings, an outward symbol of their voluntary commitment to one another."
                Master(Happy):: "Therefore, it is my pleasure, that I now pronounce them <Player.husband_or_wife> and <CurrentCompanion.husband_or_wife>."
                
                If CurrentCompanion.isMale() && Player.isMale()
                    Master(Happy):: "You may now kiss your husband!"
                Else
                    Master(Happy):: "You may now kiss your bride!"
                Endif
                
                Player(Kiss):: "..."
                AnimatePair(Player, CurrentCompanion, Kissing)
                CurrentCompanion(Kiss):: "..."
                Master.hide()
                setBackground(home)
                "And that's it - it's official, we're married! <CurrentCompanion.name> is now my dear <CurrentCompanion.husband_or_wife>."
                "The rest of the wedding ceremony went without issues, although honestly, both of us couldn't wait for it to end and the guests to leave so that we can begin our intimate wedding night."
                karma += 20
                
                If CurrentCompanion.isDominantSex(Player)
                    Player.matchLastName(CurrentCompanion)
                Else
                    CurrentCompanion.matchLastName(Player)
                Endif
                
                CurrentCompanion.setActorVar(tag_Married, 1)
                getMarried()
                Sex(Player, CurrentCompanion)
                If HungerEnable > 0
                    Player.modifyActorVar(rk_VB_Hunger, -2)
                EndIf
                
                mood += 10
                If PlayerHasHome() && DatingHasHome()
                    CurrentCompanion.show(2)
                    Player.show(0)
                     CurrentCompanion(Happy):: "Baby, now that we're married: Why don't you move in to live with me? Surely as a married couple, we must live together ..."       
                    "Start living together?"
                    0:: "Yes"
                    1:: "No"
                    2:: "Ask <CurrentCompanion.name> to move in instead"
    
                    If 0
                    
                        "I accepted <CurrentCompanion.name>'s offer."
                        loseHome()
                        CurrentCompanion:attractiontoplayer += Random(2, 10)
                        mood += 10
                        CurrentCompanion:rapportwithplayer +=  Random(2, 5)
                        
                    Elseif 1
                    
                        "I turned down <CurrentCompanion.name>'s offer."
                        CurrentCompanion:attractiontoplayer -= Random(0, 2)
                        
                    Else
                    
                        "Actually, I think it's more appropriate that <CurrentCompanion.name> moves in with me instead. After a short discussion, <CurrentCompanion.he_or_she> accepted my offer."
                        loseHomeDating()
                        mood += 10
                        CurrentCompanion:attractiontoplayer += Random(2, 10)
                        CurrentCompanion:rapportwithplayer +=  Random(2, 5)
                        
                    Endif
                    timeout(500, move_in_with_dating) 
                    
                Endif
                
            Endif
            timeout(300, rk_VB_bachelor_Party)
            
        Endif
        
    SceneEnd() // Scene edited by Raddeck - Ravenger6660
    