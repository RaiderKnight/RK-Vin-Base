WHAT: all, -sleep, -nap
WHERE: home
WHEN: 17 - 22
WHO: Dating = getSpecific(Dating); Actor = getSpecific(Dating_Friend); If Random(0, 100) < Dating:interpersonal
OTHER: isAtDatingHome()

    SceneStart()

        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        UrgeToPeeEnable = rk_VB_UrgeToPeeEnable.getGlobal()
        UrgeToPee = Player.getActorVar(rk_VB_UrgeToPee)
        Dating.dress()
        Dating.show(2)
        Dating(Happy):: "Baby, my friend <Actor.name> is coming over tonight to watch a movie. Do you want to join us?"
        0:: "Yes"
        1:: "No thanks, I will play a video game"
        2:: Player.isDominantSex(Dating) && masochist < 0:: "Tell your friend you're canceling the movie night"
        
        If 0
        
            Actor:rapportwithplayer += Random(0, 2)
            Actor:attractiontoplayer += Random(0, 2)
            Player(Excited):: "I'd love to. It would be a good opportunity to get to know your friend better."
            setBackgroundCustom(livingroom)
            "Later on ..."
            Actor.dress()
            Actor.show(3)
            "The three of us were enjoying a movie night together. We ordered pizza and had a few drinks already. I noticed that <Dating.name> had been drinking much faster than <Dating.he_or_she> normally does."
            0:: "Convince <Dating.name> to go easy on the booze"
            1:: "Let <Dating.him_or_her> be"
            
            If 0
                Player(Happy):: "Baby, you've drank too much already. You'll pass out at this rate."
                If Random(0, 100) < interpersonal
                    Dating(Sad):: "You're right. I'm well over the limit. Don't want to embarass myself in front of <Actor.name>, do I?"
                    Drunk = false
                Else
                    Dating(Irritated):: "What are you talking about? I'm not drunk, not even tipsy."
                    Drunk = true            
                Endif
            Else
                Drunk = true
            Endif
            
            If Drunk
                "Soon enough, <Dating.name> passed out from all the alcohol. <Actor.name> helped me put <Dating.name> to bed."
                Dating.hide()
                Actor(Anxious):: "I told <Dating.him_or_her> that <Dating.he_or_she> shouldn't drink so much ... We didn't even finish the movie. Oh well, I guess I'll head off and leave you two to it then."
                0:: "Ask <Actor.name> to stay"
                1:: "Go to bed"
                
                If 0
                    Player(Surprised):: "Please, don't leave. <Dating.name> might have passed out but <Dating.he_or_she> will be okay after a good night's sleep. I'm still here. Let's finish the movie at least."
                    Actor(Happy):: "As you wish then."
                    choice = -1    
                    ate = false
                    hadSex = false
                    
                    While choice < 5
                        Kissed = false
                        "What should I suggest to <Actor.name> that we do?"
                        0:: "Chat"
                        1:: !ate && stock_snack > 0.8:: "Eat something together"
                        2:: stock_movie > 0.8 :: "Watch a movie"
                        3:: stock_alcohol >= 1.8 :: "Drink alcohol"
                        4:: !hadSex:: "Kiss <Actor.him_or_her>"
                        5:: "Politely ask <Actor.him_or_her> to leave"
                    
                        If 5
                            "We said goodbye and went our ways."
                            If HungerEnable > 0
                    
                                Player.modifyActorVar(rk_VB_Hunger, -1)
                        
                            EndIf
                        Else
                            If 0
                            
                                "We simply sat and chat for a while ..."
                                mood += 1
                                Actor:attractiontoplayer += 1
                                Actor:rapportwithplayer += 1
                                passTime(0.1, 0.2)
                                
                            Elseif 1
                            
                                "We made some quick snacks and enjoyed them together ..."
                                ate = true
                                If HungerEnable > 0
                    
                                    Player.modifyActorVar(rk_VB_Hunger, 5)
                        
                                EndIf
                                Actor:attractiontoplayer += 1
                                Actor:rapportwithplayer += 1
                                mood += 1
                                stock_snack -= 1
                                energy += 5
                                passTime(0.2, 0.4)
                                
                            Elseif 2
                            
                                "We watched a movie together ..."
                                stock_movie -= 1
                                mood += 1
                                Actor:attractiontoplayer += 1
                                Actor:rapportwithplayer += 1
                                If HungerEnable > 0
                    
                                    Player.modifyActorVar(rk_VB_Hunger, -0.5)
                        
                                EndIf
                                energy -= 1
                                passTime(1, 2)
                                
                            Elseif 3
                                
                                Player.animate(drinkglass)
                                Actor.animate(drinkglass)
                                "We had a drink of alcohol together ..."
                                Player:intoxication += Random(0,10)
                                Actor:intoxication += Random(0,10)
                                stock_alcohol -= 2
                                passTime(0.1, 0.2)
                                Player.animate()
                                Actor.animate()
                                If UrgeToPeeEnable > 0
                                    Player.modifyActorVar(rk_VB_UrgeToPee, 1)
                                EndIf
                                
                            Elseif 4
                                Kissed = true
                            Endif
        
                            If Kissed || [Actor.isInterestedIn(Player) && !hadSex && Actor:attractiontoplayer > Random(0, 200)]
                                AnimatePair(Player, Actor, Kissing)
                                Player(Kiss):: "..."
                                Actor(Kiss):: "..."
                                If Dating
                                    "We kissed. It started slow but soon became more passionate. It was obvious what we both wanted ..."
                                Else
                                    "We had a kiss, an awkward one at first, but soon moving towards a more passionate direction."
                                Endif
                                "Should I let this escalate further?"
                                0:: Vanilla:: "Make out"
                                1:: "Stop"
                            
                                If 0
                                    
                                    If Actor.isDating() || [Actor.isInterestedIn(Player) && [Actor.IsDominantSex(Player) || Actor:attractiontoplayer + Actor:perversion > Random(50, 200) || Actor:intoxication > 80]]
                                        
                                        "Our kiss got more and more steamy and soon we were making out, our hands all over each other."
                                        
                                        If TfGame() && Actor.isMale()
                                            "I'm a man trapped inside a woman's body. I don't think I'm gay, but here I am, about to hook up with my male date."
                                        Endif
                                        
                                        hadSex = true
                                        Player:perversion += Random(0, 1)
                                        Sex(Player, Actor)
                                        Actor.show(2)
                                        "The sex was amazing. We put our clothes back on afterwards and continued our date."
                                        If HungerEnable > 0
                    
                                            Player.modifyActorVar(rk_VB_Hunger, -1)
                        
                                        EndIf
                                        passTime(0.2, 0.5)
                                        
                                    Else
                                    
                                        "Unfortunately for me, <Actor.name> didn't seem interested in letting things escalate any further and pulled away."
                                        
                                    Endif
                                    
                                Else
                                
                                    "I decided to stop it there and not go any further."
                                    
                                Endif
                                
                            Elseif Random(0, 100) < 20
                                If Dating
                                    "After a while, we were both exhausted and went to bed together for the night."
                                    passTime(4, 8)
                                    energy += Random(50, 100)
                                    "The next morning ..."
                                    Actor(Happy):: "I've gotta go now, honey. Thanks for letting me stay the night. I can't wait to see you again soon!"
                                    Actor:attractiontoplayer += Random(0, 1)
                                Else
                                    Actor(Happy):: "I've gotta go now. Thank you for inviting me in."
                                    Player(Happy):: "Alright. Thank you for staying. Goodbye!"
                                    "We parted ways."
                                Endif
                                choice = 5            
                            Endif  
                            
                        Endif
                        
                    Endwhile  
                    
                Else
                
                    Player(Happy):: "Alright then, good night. Thank you for hanging out with us. Sorry about <Dating.name> being such a drunken mess."
                    "I'm feeling quite drunk myself. I'd rather just kick <Actor.name> out and head to bed."
                    
                    If isModEnabled(vin_NTR) && Actor.isInterestedIn(Dating)
                    
                        Actor(Happy):: "Actually, on second thought, I hope you don't mind, but I think I'm too drunk to drive back home. Would you mind if I crash on the sofa for the night?"
                        Player(Surprised):: "Oh ... sure, since you're <Dating.name>'s friend."
                        "Whatever I guess ... don't want to be rude to <Dating.name>'s friend"
                        setBackground(home)
                        "We all now sleeping..."
                        passTime(0.5, 1)
                        Actor.hide()
                        Dating.hide()
                        "After a while..."
                        "A noise suddenly wakes me up, ..."
                        "My <Dating.boyfriend_or_girlfriend>, is not in the bed ??"
                        passTime(0.5, 1)
                        
                        "Hmm, what's that strange noise coming out from the living room?"
                        0:: "Check it out"
                        1:: "Continue sleeping"
                        
                        If 0
                        
                            setBackgroundCustom(livingroom)
                            Player(Shocked):: "What the fuck!"
                            Sex(Actor, Dating)
                            setBackgroundCustom(livingroom)
                            Player.show(0)
                            Actor.show(2)
                            Dating.show(3)
                            masochist += 2
                            "I caught <Actor.name> and <Dating.name> having sex in the living room. They're more than buddies apparently."
                            0:: "Get upset"
                            1:: "Join"
                            2:: "Continue watching"
                            
                            If 0
                            
                                Player(Surprised):: "What the fuck are you two doing? <Dating.name>, you cheating <Dating.asshole_or_bitch>!"
                                Dating:attractiontoplayer -= 20
                                mood -= 15
                                arousal -= 2
                                masochist -= Random(1, 5)
                                
                            Elseif 1
                            
                                Player(Flirty):: "Looks like you guys are having fun there. Mind if I join?"
                                arousal += 1
                                perversion += 0.5
                                Player.strip()
                                Sex(Actor, Dating, Player)
                                
                            Else
                            
                                "Let's continue hiding and enjoy the show."
                                arousal += 1
                                perversion += 0.5
                                Sex(Dating, Actor)
                                masochist += 2
                                
                            Endif
                            
                        Else
                        
                            "Probably nothing, I'm too drunk to get up right now."
                            
                        Endif
                        If HungerEnable > 0
                    
                            Player.modifyActorVar(rk_VB_Hunger, -2)
                        
                        EndIf

                    Endif
                    
                Endif
                
            Endif
        
        Elseif 2
        
            Dating(Angry)::"Why you always break my parties?"
            "What should I answer?"
            0::"Sorry, I just want to be alone wit you"
            1::"That's my rules"
            2::"Shut up bitch, kneel down..."
            
            If 0
            
                Actor:attractiontoplayer += Random(0, 2)
                Actor:rapportwithplayer += Random(0, 2)
                masochist -= 1
            
            Elseif 1
            
                Actor:rapportwithplayer -= Random(0, 1)
                masochist -= 3
            
            Else
            
                Player(Evil):: "Shut up bitch, kneel down and lick that..."
                masochist -= 5
                perversion += 1
                Filter(Blowjob)
                sex(Dating, Player)
                If HungerEnable > 0
                    
                    Player.modifyActorVar(rk_VB_Hunger, -1)
                        
                EndIf
            
            Endif
            
        Else
        
            Player(Happy):: "That's not my kind of movie? Don't worry about me, I'm going to play some video games or work on something in the next room!"
            intelligence += 1
            
        Endif
    
    SceneEnd() // Scene improved by Raddeck
    timeout(500, dating_friends_watch_show)