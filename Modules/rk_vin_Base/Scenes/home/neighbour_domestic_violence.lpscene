WHAT: all, -sleep, -nap
WHERE: home
WHEN: 0 - 24
WHO: Actor = getSpecific(Neighbour); SO = Actor.getRelatedPerson(Dating, Spouses); If [SO:martial - SO:masochist > Random(100, 200) && Actor:masochist - Actor:martial > Random(50, 100)] || [Actor:martial - Actor:masochist > Random(100, 200) && SO:masochist - SO:martial > Random(50, 100)]
OTHER: isAtHome()

    Perp = SO
    Victim = Actor
    If Actor:masochist < SO:masochist
        Perp = Actor
        Victim = SO
    Endif

    SORefer = "<SO.boyfriend_or_girlfriend>"
    VictimCallPerp = "<Perp.boyfriend_or_girlfriend>"
    If Actor.isMarried()
        SORefer = "<SO.husband_or_wife>"
        VictimCallPerp = "<Perp.husband_or_wife>"
    Endif
    If Actor.isSameGender(Dating)
        SORefer = "partner"
        VictimCallPerp = "partner"
    Endif

    SceneStart()
    SO.dress()
    SO.show(2)
    Actor.dress()
    Actor.show(3)

    "I was walking towards my apartment when I saw my neighbour <Actor.name> and <Actor.his_or_her> <SORefer> <SO.name>. To my dismay, <Perp.name> was hitting <Victim.name> ..."
    Perp(Furious):: "You bitch! How dare you text your ex?"
    Victim(Crying):: "Please stop. It hurts. You're going to kill me. I would never text my ex again ..."
    "Surely I must do something!"
    0:: "Ignore"
    1:: "Step in"
    2:: "Call the cops"

    If 0
        "It's their business. Let's not risk getting caught in a crossfire."
        masochist += Random(0, 0.5)
        karma -= 2.5
    Else
        If 1
            masochist -= Random(0, 1)
            Perp:rapportwithplayer -= Random(0, 5)
            Perp:attractiontoplayer -= Random(0, 5)
            Victim:rapportwithplayer += Random(0, 10)
            Victim:attractiontoplayer += Random(0, 10)

            Player(Angry):: "<Perp.name>! Stop right there. This is domestic violence!"
            karma += 2.5
            Perp(Angry):: "It's none of your business. Fuck off or I'll kick your ass too."
            Player.animate(martialart)
            Player.moveToPersonStand(Perp, 100)
            Player(Angry):: "No way, if you want to harm <Victim.name> anymore, you'll have to step over my dead body."
            faceEachOther(Perp, Player)
            Perp.animate(martialart)
            Perp(Furious):: "So be it! Take this!"
            Player.animate()
            Perp.animate()
            If martial > Perp:martial
                Perp.animate(fightlost)
                "I dodged <Perp.name>'s punch easily and went in for a counter strike right on <Perp.his_or_her> face, which knocked <Perp.him_or_her> on the ground."
                masochist -= Random(0, 1)
                martial += Random(0, 1)
            Else
                Player.animate(fightlost)
                "Despite my best intention, <Perp.name> was more than a match in a fight and I eventually got knocked off my feet. Thankfully, at least I kept <Perp.him_or_her> busy long enough for others to arrive and eventually control the situation."
                Player.animate()
                setBackground(hospital)
                fitness -= Random(0, 2)
                attractiveness -= Random(0, 2)
                energy -= 100
            Endif

            Perp.hide()
            Victim(Sad):: "That was so brave of you. I don't know how I could ever thank you for saving me."
        Else
            "I'm not so confident that I can stop <Perp.name> myself, but the least I could do is to call the cops."
            karma += 2.5

            Perp:rapportwithplayer -= Random(0, 5)
            Perp:attractiontoplayer -= Random(0, 5)
            Victim:rapportwithplayer += Random(0, 5)
            Victim:attractiontoplayer += Random(0, 5)
            Victim:attractiveness -= Random(0, 2)
            Victim:fitness -= Random(0, 2)

            "Eventually, the cops arrived and arrested <Perp.name>, but not before <Perp.he_or_she> already did some serious damage on <Victim.name>."
            Perp.hide()
            Victim(Sad):: "Thank you for calling the cops. It could have been a lot worse ..."
        Endif

        "Should I advise <Victim.name> to leave <Perp.name>?"
        0:: "Yes"
        1:: "No"

        If 0
            Player(Sad):: "You know, you don't have to live with all these abuses. Domestic violence cannot be tolerated. Leave <Perp.name> as fast as you can!"
            If Random(0, 100) < interpersonal && Victim:masochist < Random(0, 100)
                Victim(Sad):: "You're right ... enough is enough. I need to stand up for myself and not let my life be destroyed by a violent <VictimCallPerp>. I'm kicking <Perp.name> out for good and getting a restraining order."
                Victim:rapportwithplayer += Random(0, 5)
                Victim:attractiontoplayer += Random(0, 5)

                removeNpcRelationship(Victim, Perp)
                If Perp.isNeighbour()
                    Perp.removeNeighbour()
                Endif
            Else
                Victim(Sad):: "<Perp.name> isn't so bad. <Perp.He_or_She> just has a bad temper issue. I'm used to it by now ..."
                "Oh well, at least I tried."
            Endif

            If !Victim.isContactExchanged()
                "Before I leave, <Victim.name> asked me if <Victim.he_or_she> can call me sometimes. We then exchanged numbers."
                exchangeContact(Victim)
            Endif
        Else
            "I'm no relationship counselor. Better not get involved in other people's business more than I already did."
            masochist += Random(0, 0.5)
        Endif
    Endif

    timeout(500, neighbour_domestic_violence)
    SceneEnd()