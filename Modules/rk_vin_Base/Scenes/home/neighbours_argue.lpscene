WHAT: all, -sleep, -nap
WHERE: home
WHEN: 0 - 24
WHO: Actor = getSpecific(Neighbour); If !Actor.isAsexual() && Actor:masochist < Random(-100, 0)
OTHER: isAtHome()

    Dating = Actor.getRelatedPerson(Dating, Spouses)
    If !Dating.isValid()
        Dating = generatePersonTemporary()
        While !Actor.isInterestedIn(Dating)
            Dating = generatePersonTemporary()
        Endwhile
        Dating.makePermanent()
        addNpcRelationship(Dating, Actor, Dating)
    Endif

    SORefer = "<Dating.boyfriend_or_girlfriend>"
    If Actor.isMarried()
        SORefer = "<Dating.husband_or_wife>"
    Endif
    If Actor.isSameGender(Dating)
        SORefer = "partner"
    Endif

    SceneStart()
    Actor.dress()
    Actor.show(2)

    "I was walking just outside my apartment when I spotted my neighbour <Actor.name> sitting on the stairs, looking quite depressed."
    Player(Surprised):: "<Actor.name>? What are you doing out here all alone?"
    Actor(Sad):: "It's nothing. Don't worry ..."
    Player(Happy):: "Come on, it's okay. You can tell me."
    Actor(Sad):: "It's just that ... my <SORefer> and I, we had an argument again."

    0:: "Wish <Actor.him_or_her> luck"
    1:: "Invite <Actor.him_or_her> inside"

    If 0
        Player(Sad):: "Oh no, that's horrible to hear. But these things happen in relationships. I hope you guys will figure it out soon."
        Actor(Sad):: "I hope so too ..."
    Else
        Player(Sad):: "Oh no, that's horrible to hear. Do you want to come in my apartment and chat for a while? You shouldn't stay here all alone."
        Actor(Sad):: "I guess you're right. I don't really know where else to go right now anyway. Thank you!"
        "I opened my apartment door and invited <Actor.name> in."
        Actor:rapportwithplayer += Random(0, 5)
        Actor:attractiontoplayer += Random(0, 5)

        choice = -1
        ate = false
        hadSex = false

        While choice < 5
            Kissed = false
            "What should I suggest to <Actor.name> that we do?"
            0:: "Chat"
            1:: !ate:: "Eat something together"
            2:: "Watch a movie"
            3:: "Drink alcohol"
            4:: !hadSex:: "Kiss <Actor.him_or_her>"
            5:: "Politely ask <Actor.him_or_her> to leave"

            If 5
                "We said goodbye and went our ways."
            Else
                Actor:rapportwithplayer += Random(0, 0.5)
                Actor:attractiontoplayer += Random(0, 0.5)
                If 0
                    "We simply sat and chat for a while ..."
                Elseif 1
                    "We made some quick snacks and enjoyed them together ..."
                    ate = true
                Elseif 2
                    "We watched a movie together ..."
                Elseif 3
                    "We had a drink together ..."
                    Player:intoxication += Random(0,10)
                    Actor:intoxication += Random(0,10)
                Elseif 4
                    Kissed = true
                Endif

                If Kissed || [!hadSex && Actor.isInterestedIn(Player) && Actor:attractiontoplayer > Random(30, 200)]
                    AnimatePair(Player, Actor, Kissing)
                    Player(Kiss):: "..."
                    Actor(Kiss):: "..."
                    "We had a kiss, an awkward one at first, but soon moving towards a more passionate direction."
                    "Should I let this escalate further?"
                    0:: "Make out"
                    1:: "Stop"

                    If 0
                        If Actor.isInterestedIn(Player) && [Actor.IsDominantSex(Player) || Actor:attractiontoplayer + Actor:perversion > Random(50, 200) || Actor:intoxication > 80]
                            "Our kiss got more and more steamy and soon we were making out, our hands all over each other."
                            hadSex = true
                            Player:perversion += Random(0, 1)
                            Sex(Player, Actor)
                            Actor.show(2)
                            "The sex was amazing. We put our clothes back on afterwards and continued our date."
                        Else
                            "Unfortunately for me, <Actor.name> didn't seem interested in letting things escalate any further and pulled away."
                        Endif
                    Else
                        "I decided to stop it there and not go any further."
                    Endif
                Elseif Random(0, 100) < 20
                    Actor(Happy):: "I've gotta go now. Thank you for inviting me in."
                    Player(Happy):: "Alright. Thank you for staying. Goodbye! Good luck with your <SORefer>."
                    "We parted ways."
                    choice = 5
                Endif
            Endif
        Endwhile
    Endif

    SceneEnd()
    timeout(200, neighbours_argue)