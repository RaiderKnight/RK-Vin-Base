WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

SceneStart()

        // Get current items inside pantry inventory
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        stock_PlainPopcorn = Player.getActorVar(stock_PlainPopcorn)
        stock_ButterPopcorn = Player.getActorVar(stock_ButterPopcorn)
        stock_PopcornMix = Player.getActorVar(stock_PopcornMix)
        stock_HealthyPopcorn = Player.getActorVar(stock_HealthyPopcorn)
    
        stock_popcorn = stock_PlainPopcorn + stock_ButterPopcorn + stock_PopcornMix + stock_HealthyPopcorn
    
        stock_Pita = Player.getActorVar(stock_Pita)
        stock_Bread = Player.getActorVar(stock_Bread)
        stock_Bagel = Player.getActorVar(stock_Bagel)
        stock_BaltikBread = Player.getActorVar(stock_BaltikBread)
        stock_ProteinCookie = Player.getActorVar(stock_ProteinCookie)
        stock_VdolkyDonut = Player.getActorVar(stock_VdolkyDonut)
        stock_ApplePie = Player.getActorVar(stock_ApplePie)
        stock_RoyalCake = Player.getActorVar(stock_RoyalCake)
        stock_cocktail = Player.getActorVar(rk_VB_stock_cocktail)
        VB_stock_alcohol = stock_alcohol + stock_cocktail
        stock_bakery = stock_Pita + stock_Bread + stock_Bagel + stock_BaltikBread + stock_ProteinCookie + stock_VdolkyDonut + stock_ApplePie + stock_RoyalCake
        
        pantry_inventory = stock_popcorn + stock_bakery + stock_coffee + stock_soda + stock_tea + stock_snack + VB_stock_alcohol + stock_ingredient
    
    If pantry_inventory < 1

        Player(Anxious)::"I got nothing inside my pantry!"
        
    Else
    
        stayInMenu = true
        While stayInMenu

            "What I should do?"
            0::"Check inventory level"
            1::"Eat something"
            2::"Drink something"
            99::"Go back !"
            
            If 0
            
                Player(Curious)::"Let see what I got in my pantry"
                1:: VB_stock_alcohol > 0 ::"Check Alcohols (<stock_alcohol>)"
                0:: stock_coffee >= 0.8 ::"You have (<stock_coffee>) Coffee"
                0:: stock_soda >= 0.8 ::"You have (<stock_soda>) Soda"
                0:: stock_tea >= 0.8 ::"You have (<stock_tea>) Tea"
                0:: stock_ingredient >= 0.8 ::"You have (<stock_ingredient>) Ingredient"
                0:: stock_snack >= 0.8 ::"You have (<stock_snack>) Snack"
                20:: stock_popcorn > 0 ::"Check Popcorn inventory (<stock_popcorn>)"
                10:: stock_bakery > 0 ::"Check Bread/pastry inventory (<stock_bakery>)"
                99::"Go back !"
                
                If 20
        
                    If stock_popcorn  < 1
                        Player(Anxious)::"Ok, I don't have any popcorn! I have to go shopping !"
                    Else
        
                        0::"<stock_PlainPopcorn> bag(s) of plain popcorn"
                        0::"<stock_ButterPopcorn> bag(s) of butter popcorn"
                        0::"<stock_PopcornMix> bag(s) of popcorn sweet mix"
                        0::"<stock_HealthyPopcorn> bag(s) of healthy popcorn"
                        9:: "Go back !"
                
                    Endif
                
                ElseIf 1
        
                    If VB_stock_alcohol  < 1
                        Player(Anxious)::"Ok, I don't have any alcohol! I have to go shopping !"
                    Else
            
                        0::"<stock_alcohol> bottle(s) of alcohol"
                        0::"<stock_cocktail> cocktail"
                        99:: "Go back !"
                    
                    Endif
    
                Elseif 10
    
                    if stock_bakery  < 1
                        Player(Anxious)::"Ok, I don't have any bread or pastry! I have to go to the bakery shop!"
                    Else
                
                        0:: "<stock_Pita> pita(s)"
                        0:: "<stock_Bread> Nutricious bread(s)"
                        0:: "<stock_Bagel> Whole grain bagel(s)"
                        0:: "<stock_BaltikBread> Baltik bread(s)"
                        0:: "<stock_ProteinCookie> Protein cookie(s)"
                        0:: "<stock_VdolkyDonut> Vdolky donut(s)"
                        0:: "<stock_ApplePie> piece Homemade apple pie(s)"
                        0:: "<stock_RoyalCake> piece of Royal cake(s)"
                        99:: "Go back !"
                
                    Endif
                
                EndIf
            
            ElseIf 1
            
                48:: stock_snack >= 0.8 ::"Eat a snack"
                50:: stock_popcorn > 0 ::"Eat a popcorn"
                60:: stock_bakery > 0 ::"Eat a bread/pastry"
                99::"Nevermind, I will eat later !"
                
                If 48
            
                    Player.animate(eat)
                    "I decided to have a snack..."
                    stock_snack -= 1
                    mood += 1
                    energy += 5
                    If HungerEnable > 0
                        Player.modifyActorVar(rk_VB_Hunger, 5)
                    EndIf
                    passTime(0.15, 0.5)
 
                ElseIf 50
            
                    followUp(eat_popcorns_in)
                    stayInMenu = false
                
                ElseIf 60
            
                    followUp(eat_bakery_in)
                    stayInMenu = false
                    
                EndIf

            ElseIf 2

                80:: VB_stock_alcohol > 0 ::"Drink alcohol"
                81:: stock_coffee >= 0.8 ::"Drink coffee"
                83:: stock_soda >= 0.8 ::"Drink soda"
                84:: stock_tea >= 0.8 ::"Drink tea"
                99::"Nevermind, I will drink later !"
                
                If 80
            
                    "What should I drink?"
                    0:: stock_alcohol >= 0.8 ::"Drink alcohol"
                    11:: stock_cocktail >= 0.8 ::"Drink cocktail"
                    99::"Go back!"
                    
                
                    If 0
                
                        Player.animate(drinkbottle)
                        "I decided to drink alcohol..."
                        stock_alcohol -= 1
                        intoxication += 15
                        mood -= 1
                        energy += 0.5
                        If HungerEnable > 0
                            Player.modifyActorVar(rk_VB_Hunger, 0.5)
                        EndIf
                        passTime(0.15, 0.5)
                
                    ElseIf 11
                
                        Player.animate(drinkglass)
                        "I decided to drink a cocktail..."
                        Player.modifyActorVar(rk_VB_stock_cocktail, -1)
                        mood += 1
                        energy += 1
                        intoxication += 15
                        If HungerEnable > 0
                            Player.modifyActorVar(rk_VB_Hunger, 0.5)
                        EndIf
                        passTime(0.15, 0.5)
                        
                    EndIf
                    
                ElseIf 81
                    
                    Player.animate(drinktea)
                    "I decided to drink a coffee..."
                    stock_coffee -= 1
                    mood += 2
                    energy += 10
                    If HungerEnable > 0
                        Player.modifyActorVar(rk_VB_Hunger, 5)
                    EndIf
                    passTime(0.15, 0.5)
                    
                ElseIf 83
                    
                    Player.animate(drinkbottle)
                    "I decided to drink a soda..."
                    stock_soda -= 1
                    mood += 1
                    fitness -= 0.02
                    energy += 0.5
                    If HungerEnable > 0
                        Player.modifyActorVar(rk_VB_Hunger, 0.5)
                    EndIf
                    passTime(0.15, 0.5)
                    
                ElseIf 84
                    
                    Player.animate(drinktea)
                    "I decided to drink a tea..."
                    stock_tea -= 1
                    mood += 2
                    energy += 3
                    If HungerEnable > 0
                        Player.modifyActorVar(rk_VB_Hunger, 3)
                    EndIf
                    passTime(0.15, 0.5)
                
                EndIf

            ElseIf 99
            
                stayInMenu = false
        
            Endif
            
        EndWhile
        
    Endif

SceneEnd()