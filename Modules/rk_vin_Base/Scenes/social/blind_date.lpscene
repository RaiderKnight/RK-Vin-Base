WHAT: all, -sleep, -nap
WHERE: all
WHEN: 17 - 20
WHO: Actor = getPerson(true); If Random(0, 100) < Actor:interpersonal && !Actor.isInterestedIn(Player) && Actor:rapportwithplayer > 40
OTHER: !Player.isDating() && !isWithCompanion()

    SceneStart()
    "My phone is ringing. It's <Actor.name>. I wonder what <Actor.he_or_she> is calling me for ..."
    Actor.dress()
    SecondScreen(Actor)
    Actor.show(2)
    Date = generatePersonTemporary()
    While !Date.isInterestedIn(Player) || !Player.isInterestedIn(Date)
        Date = generatePersonTemporary()
    Endwhile
    
    Actor(Happy):: "Listen, have you gotten tired of the single life yet? There's this <Date.guy_or_girl> that I'm 100% certain is your type. I was thinking about setting up a blind date for you two ..."
    0:: "Yes"
    1:: "No"
    
    If 0
        Actor:rapportwithplayer += Random(0, 2)
        Date.makePermanent()
        Player(Wink):: "Fancy yourself a bit of a matchmaker? Oh well, I guess I'll give it a shot. But if it turns out to be the worst date in my life, I'm blaming you!"
        SecondScreen()
        Actor.hide()
        "Later on ..."
        Loc = findNearbyBuilding(restaurant)
        moveTo(Loc)
        Date.dress()
        Date.show(2)
        "My blind date introduced <Date.himself_or_herself> as <Date.name> and <Actor.name> set us up to meet each other at a restaurant nearby. <Date.He_or_She> seemed alright at first, although without knowing much about the <Date.guy_or_girl>, I didn't feel any instant connection either."
        Date.show(2)
        "Now that we've finished dinner, should I exchange contacts with <Date.name>?"
        0:: "Yes"
        1:: "No"
        
        If 0
            "We exchanged contacts so that we can go on future dates."
            exchangeContact(Date)
            "Should I continue to hang out with <Date.name> after the dinner?"
            0:: "Yes"
            1:: "No"
            
            If 0
                "Why not? I'm quite interested in getting to know my blind date better. Dinner didn't give me enough time to learn anything about the <Date.guy_or_girl>."
                Date.StartDate()
            Else
                "No, that's enough for the first date. <Date.He_or_She> has my number now so we could always go on another date some other day."
            Endif
        Else
            "I don't really like the <Date.guy_or_girl>. I'll probably never want to go on a date with <Date.him_or_her> again, so what's the point in giving <Date.him_or_her> my number?"
        Endif
    Else
        Player(Sarcastic):: "Thank you for the offer, but no, the single life is great. Wouldn't swap it for anything else!"
        Actor:rapportwithplayer -= Random(0, 1)
    Endif
        
    SceneEnd()
    timeout(200, blind_date)