WHAT: work
WHERE: work
WHEN: 7 - 11
WHO: none
OTHER: jobexperience > 20 && Random(0, 100) < 0.01*(interpersonal+jobexperience)


SceneStart()
setBackground(work)

"I've been asked to interview a candidate for the company's graduate scheme. Should I accept this opportunity?"
0:: "Yes"
1:: "No"

If 0
    "Why not? It would be an interesting experience."
    "Later on ..."
    Candidate = generatePerson()
    Candidate.blendPreset(twenties)
    Candidate.dress()
    Candidate.RandomizeHairs()
    Candidate.show(2)
    Candidate(Happy):: "It's a pleasure to meet you!"
    Player(Happy):: "The pleasure is all mine, <Candidate.name>. Shall we begin the interview?"
    If Candidate:intelligence + Candidate:interpersonal > Random(50, 150)
        "<Candidate.name> put in an impressive performance during the interview, answering all the questions I posed with ease. <Candidate.He_or_She> also seemed like a nice <Candiate.guy_or_gal> too."
        GoodCandidate = true
    Else
        "<Candidate.name> had a terrible interview, struggling to answer many questions I posed. <Candidate.He_or_She> also didn't seem to be a good fit for the company's organizational culture."
        GoodCandidate = false    
    Endif
    "How should I rate this interview later on to HR?"
    0:: "A rejection"
    1:: "A recommendation"
    2:: "Demand <Candidate.his_or_her> number for a recommendation"
    
    If 2
        Player(Flirty):: "I'll be honest with you: you performed very poorly in this interview and will surely get rejected. However, I'm willing to change my mind if you go out with me."
        If [GoodCandidate && intelligence < Random(0, 50)] || intelligence < Random(0, 100) || Candidate:attractiontoplayer > Random(0, 100)
            Candidate(Flirty):: "I guess I have no other options ..."
            "We exchanged contacts and said goodbye. I came back to the office and stayed true to my words by putting in a recommendation for the hiring of <Candidate.name>."
            If Candidate:intelligence > Random(0, 100)
                "Soon <Candidate.he_or_she> was hired and joined the company as one of my colleagues."
                Candidate.addColleague()
                exchangeContact(Candidate)
            Else
                "Despite the recommendation, mine wasn't the final round of interview and <Candidate.name> ended up still getting rejected. What a shame! I was looking forward to having <Candidate.him_or_her> as my colleague."
                "Needless to say, after getting rejected, <Candidate.name> also blocked my number."
                Candidate:rapportwithplayer -= Random(0, 30)
            Endif
        Else
            Candidate(Angry):: "Oh, that's not happening. In fact, if everyone at your company is an unprofessional sleazy pervert like you, then I'm not interested in working here anymore. Goodbye!"
            Candidate.hide()
            If Candidate:intelligence > Random(50, 100)
                "And that wasn't even the end of it. The candidate was a smart cookie - <Candidate.he_or_she> filled a complaint to my company about my indecent proposal and threatened to leak it to the press and sue the company for discrimination and harassment. The company had to settle, losing quite a bit of money in the process."
                If interpersonal > Random(50, 100)
                    "Basically begging for forgiveness from my bosses, I managed to keep my job, but just barely ..."
                    jobperformance -= Random(0, 100)
                Else
                    "Needless to say, I was promptly fired over the incident. Also, my reputation in the industry was forever damaged."
                    loseJob()
                    jobexperience -= Random(0, 20)
                Endif
            Endif
        Endif
    Else
        Candidate(Happy):: "Thank you for your time. I just want to stress again how much this job means to me."
        Player(Happy):: "Thank you for coming. HR should be in touch in a few days."
        Candidate.hide()
        If 0
            "Having ended the interview, I suggested a rejection in my interview report to HR."
        Else
            "Having ended the interview, I recommended that <Candidate.name> be hired in my report to HR."
            If Candidate:intelligence > Random(0, 100)
                "Soon <Candidate.he_or_she> was hired and joined the company as one of my colleagues."
                Candidate.addColleague()
                If GoodCandidate
                    Candidate:rapportwithplayer += Random(0, 30)
                Else
                    Candidate:rapportwithplayer += Random(0, 50)                
                Endif
            Else
                "Despite the recommendation, mine wasn't the final round of interview and <Candidate.name> ended up still getting rejected. What a shame! I was looking forward to having <Candidate.him_or_her> as my colleague."
            Endif
        Endif
    Endif
      
Else
    "I made some excuses to not do it. It would be a waste of time. They asked one of my colleagues to do it instead."
Endif

SceneEnd()
timeout(500, interview_applicants)