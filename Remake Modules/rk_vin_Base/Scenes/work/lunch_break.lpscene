WHAT: all
WHERE: work
WHEN: 11-14
WHO: Boss = getSpecific(Boss)
OTHER:

    SceneStart()
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        UrgeToPeeEnable = rk_VB_UrgeToPeeEnable.getGlobal()
        UrgeToPee = Player.getActorVar(rk_VB_UrgeToPee)
        Boss_Sucker = Player.getActorVar(rk_VB_Boss_Sucker)
        "Some of my colleagues are going for a lunch break together down at a nearby lunch spot."
        0:: "Join them"
        1:: "Keep working"
        
        If 0
            dressFormal()
            energy += 20
            "I need to eat anyway ... and it's good to get along and build rapport with your colleagues."
            setBackground(cafe)
            If Boss_Sucker < 50
                "I sit down with my colleagues and enjoy a lunch together while chatting about work and life in general."
                count = 0
                While count < 5
                    Actor = getSpecific(Colleague)
                    If Actor.isValid()
                        Actor.dress()
                        Actor.show()
                        If Actor:rapportwithplayer > 0 || Actor.isInterestedIn(Player)
                            "I feel that my rapport with my colleague <Actor.name> has improved."
                            Actor:rapportwithplayer += 1
                        Else
                            "Still, I can't seem to get along with my colleague <Actor.name> any better."
                        EndIf
                        If Actor:attractiontoplayer > 10 && Actor.isInterestedIn(Player)
                            "I can't help but feel a little a bit of special chemistry developing with <Actor.name>."
                            Actor:attractiontoplayer += 1
                        EndIf
                        Backup = Actor
                    EndIf
                    count += 1
                EndWhile
            Else
                Actor = getSpecific(Colleague)
                Actor.dress()
                Actor2 = getSpecific(Colleague)
                Actor2.dress()
                Actor.show()
                Actor2.show()
                If Actor.isValid()
                    "I sit down with my colleagues and then one of them said..."
                    Actor(Annoyed)::"Uh, excuse me, no offense, but we'd like to eat alone. I don't think I invited you!"
                    Player(Sad)::"Uh, okay, sorry!"
                    "So I got up and went to sit at another table to eat my meal..."
                    "Pff! They are probably jealous because I get along with the boss!"
                    Actor.hide()
                    Actor2.hide()
                EndIf
            EndIf
            If HungerEnable > 0
                Player.modifyActorVar(rk_VB_Hunger, 15)
            EndIf
        Else
            energy -= 10
            jobperformance += 1
            "I'm sure the boss will eventually notice how hard-working I am by skipping lunch and risking being anti-social for the sake of work."
            Player.modifyActorVar(rk_VB_Boss_Sucker, 1)
        EndIf
    SceneEnd() // Improved by Raddeck
    timeoutPrecise(24, lunch_break)