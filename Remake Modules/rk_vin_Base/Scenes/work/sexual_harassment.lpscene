WHAT: all
WHERE: work, police
WHEN: 7-22
WHO: Harasser = getSpecific(Colleague); If random(0, 100) < 10 && Harasser.isInterestedIn(Player) && [Harasser.isDominantSex(Player) || Harasser:perversion > random(70, 100)] && Harasser:perversion > random(50, 100) && Harasser:attractiontoplayer > random(0, 30)
OTHER: none

    SceneStart()
        tagPolice = Policier.getActorVar(tag_Police)
        JobCopEvent = rk_VB_JobCopEvent.getGlobal()
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        UrgeToPeeEnable = rk_VB_UrgeToPeeEnable.getGlobal()
        UrgeToPee = Player.getActorVar(rk_VB_UrgeToPee)
        If WHERE == police
            setBackground(police)
            Harasser = getSpecific(tagPolice)
            // Harasser = getPerson(tagPolice)
            If !Harasser.isValid()
                choco = random(10, 100)
                choco1 = random(10, 100)
                Harasser = generatePerson()
                Harasser:age => random(18, 55)
                Harasser.randomizeFace()
                Harasser.randomizeRace()
                Harasser.randomizeHairs()
                Harasser.dressUniform(police)
                Harasser.setActorVar(tag_Police, 1)
                Harasser.addColleague()
                Harasser:sneak => random(10, 100)
                Harasser:attractiontoplayer => random(0, 30)
                Harasser:perversion => random(50, 100)
                Harasser:masochist => random(-80, 0)
                Harasser.setActorVar(rk_VB_PoliceXP, choco)
                Harasser.setActorVar(rk_VB_PoliceSXP, choco1)
                Harasser.makeInterested(Player)
            EndIf
        Else
            setBackground(work)
            Harasser.dress()
        EndIf
        Harasser.show(2)
        Harasser(Flirty):: "Oh look at you! Aren't you an eye candy? You have no idea how much having you in the office brightens up my days, although I would much rather have you in a more private space, if you catch my meaning ..."
        "Oh there <Harasser.he_or_she> is again, getting all flirtatious and handsy."
        0:: "It's sexual harassment"
        1:: "I enjoy the attention"
        
        If 0 
            "This inappropriate behaviour really needs to stop, but how can I make it stop?"
            0:: "Do nothing"
            1:: masochist < 75:: "Clarify lack of interest"
            2:: masochist < 50:: "Threaten to tell HR"
            3:: masochist < 25:: "Report to HR"
            
            If 0
                "<Harasser.He_or_She> might be a bit unprofessional, but I don't want to be mean or threaten <Harasser.his_or_her> job."
                "Hopefully, <Harasser.he_or_she> doesn't take my silence as invitation."
                Harasser:attractiontoplayer += random(0, 2)
            ElseIf 1
                Player(Angry):: "Cut it out, <Harasser.name>! I told you many times before: I'm not interested!"
                Harasser(Flirty):: "Not yet, but you will be, eventually ..."        
            ElseIf 2
                Player(Angry):: "Get your hands off me! This is sexual harassment. If I report to HR, you'll lose your job!"
                Harasser(Scared):: "Hey, hey! Take it easy there. I haven't done anything - just paying compliments to my lovely colleague. No need to bring this up with the HR people."
                Harasser:attractiontoplayer -= random(0, 5)
            Else
                "Enough is enough! I'm telling HR all about this pervert."
                Harasser.hide()
                "A few days later ..."
                If Harasser:interpersonal > random(0, 100)
                    "'Thank you for your e-mail. Rest assured, we take all accusations of sexual harassment in the workplace very seriously. As such, we have brought up the issue with <Harasser.name>. While we have not found enough evidence for a sackable offence, we have requested that <Harasser.name> restrained from any further actions that may make <Harasser.his_or_her> colleagues uncomfortable'"
                    Harasser:attractiontoplayer -= random(0, 30)
                    Harasser:rapportwithplayer -= random(0, 30)
                    "<Harasser.He_or_She> didn't get fired, but that should be enough of a warning. I hope the pervert leaves me alone from now on ..."
                Else
                    "Great news! HR investigated the issue. Apparently I wasn't the only one being harassed by <Harasser.name> - other victims had come forward too. As such, they had no choice but to fire the pervert. Good riddance!"
                    Harasser.removeColleague()
                    Harasser:attractiontoplayer -= random(0,100)
                    Harasser:rapportwithplayer => -100
                EndIf
            EndIf
        ElseIf 1
            "I must say though: I do enjoy the attention. Besides, <Harasser.name> is all talk and no action. No harm has ever been done. I'll just leave <Harasser.him_or_her> be ..."
            Harasser:attractiontoplayer += random(0, 5)
            perversion += random(0, 0.5)
            If perversion > random(0, 100)
                "Or ... maybe I should just give the pervert what <Harasser.he_or_she> obviously wants and be done with it!"
                0:: !Harasser.isContactExchanged()::"Give <Harasser.name> my number"
                1:: "Do nothing"
                
                If 0
                    Player(Flirty):: "Look at you, all talk and no action. Why don't you actually invite me out some time?"
                    "With that said, I slipped a piece of paper to <Harasser.name> with my personal number on it. The pervert couldn't believe <Harasser.his_or_her> luck!"
                    exchangeContact(Harasser)
                Else
                    "Actually, that is a very bad idea. It's annoying enough having to deal with such a weirdo at work - I'm not going to invite <Harasser.him_or_her> into my private life."
                EndIf
            EndIf
        EndIf
        timeout(550, sexual_harassment)
    SceneEnd() // Improved by Raddeck