WHAT: all, -sleep, -nap, -sleep_hotel, -nap_hotel
WHERE: all
WHEN: 0 - 24
WHO: Dating = getSpecific(Dating); Ex = getSpecific(ExDating); If Ex.isContactExchanged() && Ex:attractiontoplayer > 40 && Dating:attractiontoplayer > 40
OTHER: !isWithCompanion() && !OpenRelationship()

    SceneStart()
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        Dating.dress()
        Ex.dress()
        Dating.show(2)
        Ex.show(3)
        "<Dating.name> and my ex <Ex.name> clearly don't like each other. <Dating.name> has not stopped accusing <Ex.name> of still harboring romantic intention of getting back with me, while <Ex.name> couldn't believe I ended up with someone like <Dating.name> after <Ex.him_or_her>."
        0:: "Spend more time with <Dating.name>, my <Dating.boyfriend_or_girlfriend>"
        1:: "Be more flirtatous to <Ex.name>, my ex"
        2:: "Try to mediate"
    
        If 0
            "I decided to be on <Dating.name>'s side on this and spent more time with <Dating.him_or_her>. <Dating.He_or_She> is my <Dating.boyfriend_or_girlfriend> after all! Of course, this also sent a message to <Ex.name>."
            Dating:attractiontoplayer += Random(0, 5)
            Ex:attractiontoplayer -= Random(0, 5)
            
        Elseif 1
            "I decided to be on <Ex.name>'s side on this and openly flirted with <Ex.him_or_her> many times. Of course, my blatant betrayal clearly upset <Dating.name>."
            Dating:attractiontoplayer -= Random(0, 5)
            Ex:attractiontoplayer += Random(0, 5)
            
        Else
            "I'd rather not have to choose. I tried instead to find a resolution to their conflict."
            If Random(0, 200) < interpersonal
            
                "My effort worked out, their relationship has improved somewhat. They still aren't thrilled to see each other but at least they have stopped going at each other all the time."
                Dating:attractiontoplayer += Random(0, 2)
                Ex:attractiontoplayer += Random(0, 2)
                
                If perversion > 50
                
                    "Should I be satisfied with this improvement or should I push for even more? A perverted idea comes up in my head"
                    0:: "A threesome should resolve all conflicts!"
                    1:: "Let's play strip poker!"
                    2:: "That's a stupid ideas"
                    
                    If 0
                    
                        "To be honest, I have always fantasized about getting sandwiched between my <Dating.boyfriend_or_girlfriend> and my ex ... Besides, maybe a threesome would be just what needed to stop these two from being so possessive of me."
                        If Random(50, 100) < Dating:perversion && Random(50, 100) < Ex:perversion
                        
                            "Somehow I managed to convince them both to do it ... Sharing is caring after all."
                            setBackground(home)
                            Sex(Player, Dating, Ex)
                            perversion += Random(0, 1)
                            Dating:attractiontoplayer += Random(0, 2)
                            Ex:attractiontoplayer += Random(0, 2)
                            If HungerEnable > 0
                                Player.modifyActorVar(rk_VB_Hunger, -3)
                            EndIf
                    
                        Else
                            "Unfortunately, as fantastic as the idea appears in my head, I didn't even get close to convicing them to go for it."
                        Endif
                        
                    Elseif 1
                        // From co_vi_strippoker.lpscene - sb_PartyAndNightlife
                        Player(Embarrassed):: "Okay then ... Let's play."
                        Player(Flirty):: "Remember our usual rules: If anyone loses a round but has no clothes left, they have to touch themselves a bit for everyone else's enjoyment."
                        Dating(Happy):: "Deal!"
                        Ex(Happy):: "Sounds good to me."
                        round = 0
                        perversion += Random(0, 0.5)
                        masochist += Random(0, 0.25)
                        While choice == 0 && round < 20
                            "What should I do?"
                            0:: "Play another round"
                            1:: "Stop playing"
            
                            If 0
                                round += 1
                                Random
                                    Loser = Player
                                    Loser = Dating
                                    Loser = Ex
                                endRandom
                                Loser(Sad):: "Damn, I lost!"
                                If Loser.isNaked()
                                    Loser(Posing):: "No clothes left ... Some solo action it is then ..."
                        
                                    Player.hide()
                                    Dating.hide()
                                    Ex.hide()
                                    Filter(Solo)
                                    Sex(Loser)
                                    Player.show(0)
                                    Dating.show(4)
                                    Ex.show(1)
                        
                                    Loser:perversion += Random(0, 0.25)
                                    If HungerEnable > 0
                                        Player.modifyActorVar(rk_VB_Hunger, -1)
                                    EndIf
                                    
                                Else
                                    Loser(Embarrassed):: "Off this goes then ..."
                                    Loser.stripOne()
                                    Loser:perversion += Random(0, 0.1)
                                    Loser:arousal += Random(0, 1)
                                Endif
                            Endif
                        Endwhile
                        
                        DatingEX = Dating.isInterestedIn(Ex)
                        EXDating = Ex.isInterestedIn(Dating)
                        If Dating:arousal > 25 && Ex:arousal > 25 && DatingEX && EXDating
                        
                            Dating(Flirty)::"What do you think guys, if we finish that game in beauty with sex actions...?"
                            Player(Happy):: "Deal!"
                            Ex(Happy):: "Sounds good to me."
                            Player.strip()
                            Dating.strip()
                            Ex.strip()
                            "We undressed right away, and started to caress each other."
                            Sex(Player, Dating, Ex)
                            perversion += Random(0, 1)
                            Dating:attractiontoplayer += Random(0, 2)
                            Ex:attractiontoplayer += Random(0, 2)
                            Player.dress()
                            Dating.dress()
                            Ex.dress()
                            Ex(Happy)::"That was really amazing! But I need to leave now! See you soon guys!"
                            Ex.hide()
                            If HungerEnable > 0
                                Player.modifyActorVar(rk_VB_Hunger, -3)
                            EndIf
                        
                        Else
                            
                            Player(Happy):: "I think that's enough strip poker for now ..."
                            mood += 3
                            Player.dress()
                            Dating.dress()
                            Ex.dress()
                            Ex(Happy)::"That was really fun! But I need to leave now! See you soon guys!"
                            Ex.hide()
                            
                        Endif
                    
                    Else
                    
                        "Nah, not really. I must have been watching too much porn recently to even think about that."
                        
                    Endif
                    
                Endif
                
            Else
            
                "My effort failed miserably. Not only did their relationship not get any better, they were both annoyed that I tried to make them friends and refused to pick a side."
                Dating:attractiontoplayer -= Random(0, 5)
                Ex:attractiontoplayer -= Random(0, 5)
                mood -= 3
                If HungerEnable > 0
                    Player.modifyActorVar(rk_VB_Hunger, -0.25)
                EndIf
                
            Endif
            
        Endif

    timeout(500, dating_and_ex)
    SceneEnd() // Scene improved by Raddeck