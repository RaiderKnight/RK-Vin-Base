WHAT: none
WHERE: none
WHEN: 0-24
WHO: none
OTHER: none

    SceneStart()
        If isModEnabled(rk_vin_Base)
            CurrentCompanion.show()
            Hunger = Player.getActorVar(rk_VB_Hunger)
            HungerEnable = rk_VB_EnableHunger.getGlobal()
            FacebookFriend = rk_VB_FacebookFriend.getGlobal()
            UrgeToPeeEnable = rk_VB_UrgeToPeeEnable.getGlobal()
            UrgeToPee = Player.getActorVar(rk_VB_UrgeToPee)
            CEngaged = true
            NPCisEngaged = rk_VB_isEngaged.getGlobal()
            If CEngaged || NPCisEngaged > 0
                Cheated = false
                CurrentCompanion.hide()
                setBackground(home)
                "With our wedding coming up, should I have a <Player.bachelor_or_bachelorette> party without my fiance?"
                0:: "Yes"
                1:: "No"
                
                If 0
                    "Why not? I might not have many more chances to let loose after our wedding."
                    "With that decision made, one of my best friends, a party animal, immediately volunteered to organize a <Player.bachelor_or_bachelorette> party for us."
                    "<Player.He_or_She> didn't tell me the details of what's going to happen on the day, but told me to expect some pleasant surprises."
                    setBackground(nightclub)
                    "Tonight is the night. My friend got us a nice limousine to take us to the most expensive nightclub in the city. We had so much booze on the limousine that by the time we arrived at the club, we were already quite drunk."
                    "I can't believe it! My friend managed to book the nightclub's VIP area all for us. This must have cost a fortune! I knew I could rely on my friend ..."
                    "After many more rounds of expensive alcohol, I was told to close my eyes for the surprise of the night that my friends had in store for me. Being quite drunk already, I happily obliged."
                    Stripper = generatePersonTemporary()
                    While !Player.isInterestedIn(Stripper)
                        Stripper = generatePersonTemporary()
                    EndWhile
                    Stripper2 = generatePersonTemporary()
                    While !Player.isInterestedIn(Stripper2)
                        Stripper2 = generatePersonTemporary()
                    EndWhile
                    Stripper.blendPreset(twenties)
                    Stripper2.blendPreset(twenties)
                    
                    If Stripper.isMale()
                        Stripper.blendPreset(bodybuilder)
                    Else
                        Stripper.blendPreset(hourglass1_F)
                    EndIf
                    Stripper.randomizeFace()
                    Stripper.randomizeHairs()
                    
                    If Stripper2.isMale()
                        Stripper2.blendPreset(bodybuilder)
                    Else
                        Stripper2.blendPreset(hourglass1_F)
                    EndIf
                    Stripper2.randomizeFace()
                    Stripper2.randomizeRace()
                    Stripper2.randomizeHairs()
                    Stripper.show(2)
                    Stripper2.show(3)
                    "My friends then told me to open my eyes, and I couldn't believe what I was seeing ... Two hot strippers are dancing in front of me with all my friends cheering them on."
                    "My best friend whispered into my ear, assuring me that the strippers have already been paid for some 'extra service'."
                    "<Player.He_or_She> suggested I have one last threesome with these hotties before being tied down to one <CurrentCompanion.man_or_woman> for the rest of my life."
                    0:: "Yes"
                    1:: "No"
                    
                    If 0
                        "Oh well, as long as <CurrentCompanion.name> doesn't find out, this would be an experience I'll never forget. People do this on <Player.bachelor_or_bachelorette> parties all the time, I'm sure ..."
                        karma -= 20
                        Sex(Stripper, Player, Stripper2)
                        Player.show(0)
                        If HungerEnable > 0
                            Player.modifyActorVar(rk_VB_Hunger, -3)
                        EndIf
                        Cheated = true
                    Else
                        "No way, this is going way too far. I must remain faithful to my fiance and not give in to peer pressure. I can't risk our marriage just for a few minutes of wild fun."
                        "Having made my decision, I sent the strippers on their way. My friends were clearly disappointed and the whole thing was a bit of a killjoy for the rest of the party."
                    EndIf
                    Stripper.hide()
                    Stripper2.hide()
                Else
                    CurrentCompanion:attractiontoplayer += random(0, 5)
                    "No, nothing good would come out of it. Let's just focus on making the proper preparations for the wedding."
                EndIf
                
                Actor = getSpecific(ExDating)
                If Actor.isValid() && Actor.isContactExchanged() && Actor:masochist > 0 && Actor:rapportwithplayer > 0 && Actor:attractiontoplayer > 20
                    setBackground(home)
                    "I heard a knock on my door. I wonder who it might be."
                    Actor.dress()
                    Actor.show(2)
                    "It's my ex <Actor.name>!"
                    Actor(Happy):: "<Player.name>, I found out from Facebook ... you're getting married to <CurrentCompanion.name> ..."
                    "As far as exes go, we're still on quite good terms, but I obviously didn't invite <Actor.name> to the wedding. Not exactly the easiest announcement to make to an ex."
                    Actor(Sad):: "I guess it's your life and you have every right to make that choice ... I'm glad you found a <CurrentCompanion.man_or_woman> to settle down with ... I'm happy for you ..."
                    "<Actor.name>'s facial expression certainly didn't scream out 'happy'. Poor <Actor.guy_or_girl>, maybe deep inside <Actor.name> still has feelings for me ... and to be honest, maybe I do feel the same."
                    0:: "Just thank <Actor.him_or_her>"
                    1:: "Invite <Actor.him_or_her> in"
        
                    If 0
                        Player(Sad):: "Thank you ... You should go though. It would be troublesome if my fiance sees you here."
                        Actor:attractiontoplayer -= random(0, 3)
                    Else
                        Player(Sad):: "Thank you ... That's very kind of you."
                        "Feeling sorry for my ex, I decided to invite <Actor.him_or_her> inside to chat a bit. I told myself nothing would happen and I was only doing it out of sympathy."
                        Player(Sad):: "Actually, do you want to come inside for a bit? We're still friends right?"
                        choice = -1    
                        ate = false
                        hadSex = false
                            
                        While choice < 5
                            Kissed = false
                            "What should I suggest to <Actor.name> that we do?"
                            0:: "Chat"
                            1:: !ate && stock_snack >= 1:: "Eat something together"
                            2:: stock_movie >= 1 :: "Watch a movie"
                            3:: stock_alcohol >= 2 :: "Drink alcohol"
                            4:: !hadSex:: "Kiss <Actor.him_or_her>"
                            5:: "Politely ask <Actor.him_or_her> to leave"
                    
                            If 5
                                "We said goodbye and went our ways."
                                If HungerEnable > 0
                                    Player.modifyActorVar(rk_VB_Hunger, -1)
                                EndIf
                            Else
                                If 0
                                    "We simply sat and chat for a while ..."
                                    mood += 1
                                    Actor:attractiontoplayer += 1
                                    Actor:rapportwithplayer += 1
                                    passTime(0.1, 0.2)
                                
                                ElseIf 1
                                    "We made some quick snacks and enjoyed them together ..."
                                    ate = true
                                    If HungerEnable > 0
                                        Player.modifyActorVar(rk_VB_Hunger, 5)
                                    EndIf
                                    Actor:attractiontoplayer += 1
                                    Actor:rapportwithplayer += 1
                                    mood += 1
                                    stock_snack -= 1
                                    energy += 5
                                    passTime(0.2, 0.4)
                                
                                ElseIf 2
                                    setBackgroundCustom(livingroom)
                                    "We watched a movie together ..."
                                    stock_movie -= 1
                                    mood += 1
                                    Actor:attractiontoplayer += 1
                                    Actor:rapportwithplayer += 1
                                    If HungerEnable > 0
                                        Player.modifyActorVar(rk_VB_Hunger, -0.5)
                                    EndIf
                                    energy -= 1
                                    passTime(1, 2)
                                
                                ElseIf 3
                                    "We had a drink together ..."
                                    Player.animate(drinkglass)
                                    Actor.animate(drinkglass)
                                    Player:intoxication += random(0,10)
                                    Actor:intoxication += random(0,10)
                                    stock_alcohol -= 2
                                    passTime(0.1, 0.2)
                                    Player.animate()
                                    Actor.animate()
                                    If UrgeToPeeEnable > 0
                                        Player.modifyActorVar(rk_VB_UrgeToPee, 1)
                                    EndIf
                                ElseIf 4
                                    Kissed = true
                                EndIf
                                If Kissed || [!hadSex && Actor:attractiontoplayer > random(30, 200)]
                                    Player(Kiss):: "..."
                                    animatePair(Player, Actor, Kissing)
                                    Actor(Kiss):: "..."
                                    "We had a kiss, an awkward one at first, but soon moving towards a more passionate direction."
                                    karma -= 2
                                    "Should I let this escalate further?"
                                    0:: "Make out"
                                    1:: "Stop"
                            
                                    If 0
                                        If Actor.isInterestedIn(Player) || Actor:intoxication > 75
                                            "Our kiss got more and more steamy and soon we were making out, our hands all over each other."
                                            hadSex = true
                                            Player:perversion += random(0, 1)
                                            karma -= 20
                                            Sex(Player, Actor)
                                            Actor.hide()
                                            Player.show(0)
                                            "The sex was amazing. We put our clothes back on afterwards and continued our date."
                                            If HungerEnable > 0
                                                Player.modifyActorVar(rk_VB_Hunger, -2)
                                            EndIf
                                            Cheated = true
                                            "Why did I do that? I let the old flame spark again ... I can only hope that my fiance will never find out about it."
                                            passTime(0.2, 0.5)
                                            choice = 5
                                        Else
                                            "Unfortunately for me, <Actor.name> didn't seem interested in letting things escalate any further and pulled away."
                                        EndIf
                                    Else
                                        "I decided to stop it there and not go any further. This could have destroyed my future marriage."
                                    EndIf
                                ElseIf random(0, 100) < 15
                                    Actor:attractiontoplayer += random(0, 1)
                                    Actor(Happy):: "I've gotta go now. Thank you for inviting me in."
                                    Actor(Happy):: "It has been a great day, but I'm getting quite tired now..."
                                    Player(Happy):: "Alright. Thank you for staying. Goodbye!"
                                    "We parted ways."
                                    choice = 5            
                                EndIf        
                            EndIf
                        EndWhile
                    EndIf
                    Actor.hide()
                EndIf
                Affair = getSpecific(Affair)
                If intelligence < CurrentCompanion:intelligence*random(0, 1) && [Cheated || Affair.isValid()]
                    setBackground(home)
                    CurrentCompanion.show(2)
                    CurrentCompanion(Angry):: "<Player.name>, we need to talk!"
                    CurrentCompanion:attractiontoplayer -= random(0, 50)
                    "This can't be good ..."
                    Player(Anxious):: "Yes, darling, what is it? You don't look too happy ..."
                    CurrentCompanion(Angry):: "Of course I don't, and you know damn well why!"
                    CurrentCompanion(Furious):: "I know about your little romp. You <Player.asshole_or_slut>, you were sleeping around behind my back! I trusted you. I loved you. I wanted to spend the rest of my life with you, yet you betrayed me!"
                    "And just like that, <CurrentCompanion.name> called off the engagement and broke up with me ... not too long before our planned wedding date. I found out later that <CurrentCompanion.he_or_she> also blocked my number."
                    CurrentCompanion.setDatingId()
                    loseDating()
                    mood -= 100
                    blockContact(Dating)
                    rk_VB_isEngaged.setGlobal(0)
                Else
                    SexCashOffer = random(2000, 5000)
                    SexCashOffer_loc = SexCashOffer.convertToLocalCurrency()
                    John = getPerson(tag_john)
                    If John.isValid() && isModEnabled(rk_vin_Base)
                        secondScreen(John)
                        John.dress()
                        John.show()
                        "Just a few days before the wedding, one of my regular customers as a sex worker, <John.name>, found out that I was getting married and called me."
                        "<John.He_or_She> congratulated me, but made me an offer that <John.he_or_she> was confident I couldn't refuse."
                        "<John.name> offers me a small fortune (<SexCashOffer_loc>) to be a wedding guest and then be the one to actually bang me on my wedding night instead of my newly-wed <CurrentCompanion.husband_or_wife> <CurrentCompanion.name> while <CurrentCompanion.name> watches."
                        If John.isInterestedIn(CurrentCompanion)
                            "In fact, once <John.he_or_she>'s done with me, <John.he_or_she> wants to fuck my new <CurrentCompanion.husband_or_wife> as well."
                        EndIf
                        secondScreen()
                        "Should I try to convince <CurrentCompanion.name> to accept <John.name>'s offer?"
                        0:: "Yes"
                        1:: "No"
                        
                        If 0
                            "I managed to convince <CurrentCompanion.name> that wedding night sex is only sentimental anyway and the money being offered could go a long way to financial security for our life together."
                            JohnFucks = true
                        Else
                            "I rejected <John.name>'s offer on our behalf."
                            JohnFucks = false
                        EndIf
                    Else
                        JohnFucks = false
                        Incalling = CurrentCompanion.getActorVar(tag_incalling)
                        If incalling > 0
                            John = generatePersonTemporary()
                            While !CurrentCompanion.isInterestedIn(John)
                                John = generatePersonTemporary()
                            EndWhile
                            secondScreen(John)
                            John.dress()
                            John.show()
                            "Just a few days before the wedding, one of <CurrentCompanion.name>'s regular customers as a sex worker, <John.name>, found out that <CurrentCompanion.he_or_she> was getting married and called <CurrentCompanion.him_or_her>."
                            "<John.He_or_She> congratulated <CurrentCompanion.him_or_her>, but made <CurrentCompanion.him_or_her> an offer that <John.he_or_she> was confident we couldn't refuse."
                            "<John.name> offers us a small fortune to be a wedding guest and then be the one to actually bang <CurrentCompanion.name> on our wedding night instead of me while I watch."
                            If John.isInterestedIn(Player)
                                "In fact, once <John.he_or_she>'s done with <CurrentCompanion.name>, <John.he_or_she> wants to fuck me as well."
                            EndIf
                            secondScreen()
                            "<CurrentCompanion.name> was tempted by the money that could go a long way to financial security for our life together. However, <CurrentCompanion.he_or_she> asked for my blessing first?"
                            0:: "Yes"
                            1:: "No"
                            
                            If 0
                                "<CurrentCompanion.name> managed to convince me that wedding night sex is only sentimental anyway and the money being offered could go a long way to financial security for our life together."
                                JohnFucks = true
                            Else
                                "I insisted that <CurrentCompanion.name> reject <John.name>'s offer on our behalf."
                                JohnFucks = false
                            EndIf
                        EndIf
                    EndIf
                    Player.show(0)
                    CurrentCompanion.show(1)
                    Player.dressUniform(Wedding)
                    CurrentCompanion.dressUniform(Wedding)
                    "So we decided to go to the church to get officially married..."
                    Master = generatePersonTemporary()
                    While !Master.isMale()
                        Master = generatePersonTemporary()
                    EndWhile
                    setBackground(place_of_worship)
                    Player.dressUniform(Wedding)
                    CurrentCompanion.dressUniform(Wedding)
                    Master.dress(Priest-Robe_1)
                    "The most important day of my life is here ..."
                    "We are so happy to be getting married... I will tell you that I can't wait for my honeymoon."
                    If CurrentCompanion.isExRelative()
                        If isQuestCompleted(legalized) && isModEnabled(vin_Incest) // Edited by Raddeck
                            "Thanks to the recent legalization of incest, we had no problem registering the marriage with the government and have a proper public wedding."
                            "Incest being legal doesn't stop some close-minded people from talking shit about us of course, but we couldn't care less about that."
                        Else
                            "Registering this marriage with the government is a no-goer - incest is illegal in this country so that would just send us straight to jail ..."
                            "Still, we wanted to have a proper wedding, so we only only invited a few particularly open-minded family and friends, as well as guests who met us after we became a couple and had no idea we were related."
                        EndIf
                    EndIf
                    Master.show(3)
                    Master(Happy):: "Hi, welcome to my two newlyweds... Get in place, we are going to start the ceremony..."
                    Master(Happy):: "Today, <Player.name> and <CurrentCompanion.name> honoured you with their invitation to be present with them today for this ceremony."
                    Master(Happy):: "And so, this day, they declare before all of us that they shall live together in marriage."
                    Master(Happy):: "In the traditional way, they entered into their marriage with the joining of hands, the making of vows and promises, and the giving and receiving of rings, an outward symbol of their voluntary commitment to one another."
                    Master(Happy):: "Therefore, it is my pleasure, that I now pronounce them <Player.husband_or_wife> and <CurrentCompanion.husband_or_wife>."
                    If CurrentCompanion.isMale() && Player.isMale()
                        Master(Happy):: "You may now kiss your husband!"
                    Else
                        Master(Happy):: "You may now kiss your bride!"
                    EndIf
                    animatePair(Player, CurrentCompanion, Kissing)
                    Player(Kiss):: "..."
                    CurrentCompanion(Kiss):: "..."
                    Player:arousal += 1
                    CurrentCompanion:arousal += 1
                    "And that's it - it's official, we're married! <CurrentCompanion.name> is now my dear <CurrentCompanion.husband_or_wife>."
                    "The rest of the wedding ceremony went without issues, although honestly, both of us couldn't wait for it to end and the guests to leave so that we can begin our intimate wedding night."
                    Master.hide()
                    "We went home to celebrate more officially..."
                    setBackground(home)
                    karma += 20
                    mood += 5
                    CurrentCompanion:rapportwithplayer += random(2, 5)
                    CurrentCompanion:attractiontoplayer += random(2, 5)
                    If JohnFucks && isModEnabled(rk_vin_Base)
                        money += SexCashOffer
                        "Of course, this is an entirely different way to consummate one's marriage, as we already agreed with my wealthy client <John.name>."
                        John.show()
                        "Having attended the wedding as a guest, <John.name> followed us to the wedding night suite ..."
                        If John.isTemporary()
                            "And just like that, <John.name> fucked my newly-wed <CurrentCompanion.husband_or_wife> while I could do nothing but watch my newly-wed <CurrentCompanion.husband_or_wife> do <CurrentCompanion.his_or_her> duty as a prostitute to a regular customer on our wedding night."
                            Sex(John, CurrentCompanion)
                            If John.isInterestedIn(Player)
                                "Of course, the arrangement didn't end there. And <John.name> proceeded to have sex with me as well."
                                "Everyone assumed that both partners have sex on their wedding night, but the assumption is that they have sex with each other. Our wedding night is certainly unique."
                                Sex(John, Player)
                                If HungerEnable > 0
                                    Player.modifyActorVar(rk_VB_Hunger, -2)
                                EndIf
                            EndIf   
                        Else
                            "And just like that, <John.name> fucked me while my newly-wed <CurrentCompanion.husband_or_wife> could do nothing but watch <CurrentCompanion.his_or_her> newly-wed <Player.husband_or_wife> do my duty as a prostitute to a regular customer on our wedding night."
                            Sex(John, Player)
                            If HungerEnable > 0
                                Player.modifyActorVar(rk_VB_Hunger, -2)
                            EndIf
                            If John.isInterestedIn(CurrentCompanion)
                                "Of course, the arrangement didn't end there. And <John.name> proceeded to have sex with my newly-wed <CurrentCompanion.husband_or_wife> as well."
                                "Everyone assumed that both partners have sex on their wedding night, but the assumption is that they have sex with each other. Our wedding night is certainly unique."
                                Sex(John, CurrentCompanion)
                            EndIf
                        EndIf
                        If John.isInterestedIn(CurrentCompanion) && John.isInterestedIn(Player)
                            John:energy += 25
                            Player:energy += 25
                            CurrentCompanion:energy += 25
                            "Of course, the session didn't end there. We decided to do a threesome..."
                            Sex(John, Player, CurrentCompanion)
                        EndIf
                    Else
                        If CurrentCompanion.isDominantSex(Player)
                            Player.matchLastName(CurrentCompanion)
                        Else
                            CurrentCompanion.matchLastName(Player)
                        EndIf
                        If HungerEnable > 0
                            Player.modifyActorVar(rk_VB_Hunger, -5)
                        EndIf
                        "Arriving at the house, it didn't take 5 minutes, that we had already started to undress to make love like sex starved."
                        Player.strip()
                        CurrentCompanion.strip()
                        Sex(Player, CurrentCompanion)
                        If HungerEnable > 0
                            Player.modifyActorVar(rk_VB_Hunger, -2)
                        EndIf
                        Player.dress()
                        CurrentCompanion.dress()
                        rk_VB_isEngaged.setGlobal(0)
                        mood += 10
                    EndIf
                    CurrentCompanion.setActorVar(tag_Married, 1)
                    getMarried()
                    If PlayerHasHome() && DatingHasHome()
                        CurrentCompanion.show(2)
                        Player.show(0)
                         CurrentCompanion(Happy):: "Baby, now that we're married: Why don't you move in to live with me? Surely as a married couple, we must live together ..."       
                        "Start living together?"
                        0:: "Yes"
                        1:: "No"
                        2:: "Ask <CurrentCompanion.name> to move in instead"
        
                        If 0
                            "I accepted <CurrentCompanion.name>'s offer."
                            loseHome()
                            CurrentCompanion:attractiontoplayer += random(2, 10)
                            mood += 10
                            CurrentCompanion:rapportwithplayer +=  random(2, 5)
                        ElseIf 1
                            "I turned down <CurrentCompanion.name>'s offer."
                            CurrentCompanion:attractiontoplayer -= random(0, 2)
                        Else
                            "Actually, I think it's more appropriate that <CurrentCompanion.name> moves in with me instead. After a short discussion, <CurrentCompanion.he_or_she> accepted my offer."
                            loseHomeDating()
                            mood += 10
                            CurrentCompanion:attractiontoplayer += random(2, 10)
                            CurrentCompanion:rapportwithplayer +=  random(2, 5)
                        EndIf
                        timeout(500, move_in_with_dating) 
                    EndIf
                EndIf
            EndIf
            endDate()
        EndIf
    SceneEnd() // Scene edited by Raddeck