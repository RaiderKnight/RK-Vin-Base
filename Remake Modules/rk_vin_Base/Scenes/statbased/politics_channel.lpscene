WHAT: none
WHERE: none
WHEN: 17 - 22
WHO: none
OTHER: none
    
    SceneStart()
    
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        ChannelStarted = politics_channel.getGlobal()
        POchannelVids = Player.getActorVar(rk_VB_POchannelVids) // Politics channel
        
        If ChannelStarted == 0
        
            "I consider myself quite up-to-date with current affairs. I can be quite eloquent when debating hot-button political issues"
            "I reckon I might just have some success starting a politics channel on YouTube. Should I?"
            0:: "Yes"
            1:: "No"
            
            If 0
            
                "Why not? It's certainly worth a shot. Might be a good way to supplement my income."
                mood += 3
                politics_channel.setGlobal(1)
                
            Else
            
                "No, I should stop daydreaming and focus on my real job."
                //timeout(500, politics_channel)
                
            Endif
            
        Else
        
            "I've got some spare time. Should I make a new video for my politics channel?"
            0:: "Yes"
            1:: "No"
            2:: "Close channel"
            
            If 0
            
                "Yes, it's important to upload regularly to keep my audience growing."
                intelligence += Random(0, 0.25)
                
                If Random(0, 100) < intelligence
                
                    "The new video turned out to be a success, earning a decent amount of views and a bit of money for myself."
                    money += Random(0.5, 5)*intelligence
                    mood += 10
                    intelligence += 0.05
                    
                Else
                
                    "Unfortunately, the new video was a miss, not earning many views at all and even less money."
                    mood -= 10
                    
                Endif
                Player.modifyActorVar(rk_VB_POchannelVids, 1)
                If HungerEnable > 0
                    Player.modifyActorVar(rk_VB_Hunger, -1)
                EndIf
                energy -= 1
                
            Elseif 1
            
                "I'm too busy right now to film. Let's do that another day."
                
            Else
            
                "This thing isn't working out. It's time to shut it down."
                politics_channel.clearGlobal()
                //timeout(500, politics_channel)
                
            Endif
            
        Endif
    
    SceneEnd()  // Scene improved by Raddeck
    // timeout(48, politics_channel)