WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER:

    SceneStart()
    If isTimingOut(take_final)
        "Too early since enrollment in the degree or since the last failed exams. Need to wait at least four in-game months since enrollment or at least one in-game month since my last failure."
    Else
        "Am I sure I'm ready to take the final exams now?"
        0:: "Yes"
        1:: "No"
        
        If 0
            clearGetList()
            timeoutPrecise(720, take_final)
            Actor2 = getSpecific(Dating)
            If Actor2.isValid()
                Actor2.dress()
            EndIf
            "Yes, I think I'm ready ..."
            tot = 0
            dressFormal()
            count = 0
            Actor = getSpecific(Lecturer)
            While Actor.isValid()
                count += 1
                Actor.dress()
                secondScreen(Actor)
                Actor.show()
                "The next exam is for the <Actor.major> subject, to be marked by Dr <Actor.name> <Actor.name_last>."
                secondScreen()
                "I've done all the revisions I could for it. The only thing left now is to take the exams. I hope that my effort pays off ..."
                cur = karma*random(0, 0.66) + intelligence*0.66
                If cur > 100
                    cur => 100
                EndIf
                If cur > 95
                    "I came out of the exam delighted. I absolutely nailed it for sure. Could easily be a perfect score even." 
                ElseIf cur > 80
                    "I'm quite confident that I did very well there. I was well-prepared for all the questions." 
                ElseIf cur > 65
                    "I think you should easily pass that, but some of the questions were more challenging than I expected."
                ElseIf cur > 50
                    "Well, that definitely wasn't a cakewalk, I think I probably did just enough to pass though."
                Else
                    "Damn, it turned out I was quite ill-prepared for that exam. A lot of things came up that I didn't revise for. Pretty sure I failed."
                EndIf
                "Now that I finished the <Actor.major> exam and have a rough idea how well I did, maybe I should plan in advance and try to intervene in the result? After all, I could visit the marker of this exam, Dr <Actor.name> <Actor.name_last>, in <Actor.his_or_her> office ..."
                0:: "Offer to fuck Dr <Actor.name> <Actor.name_last> for a better mark"
                1:: Actor2.isValid():: "Offer my <Actor2.boyfriend_or_girlfriend> <Actor2.name>'s body for a better mark"
                2:: "Do nothing"
                
                If 2
                    karma += 2
                ElseIf 0
                    Actor.show()
                    If Actor.hadSex() || [random(50, 75) < Actor:perversion && random(0, 25) < Actor:attractiontoplayer && Actor.isInterestedIn(Player)]
                        Actor:: "I really should be impartial, my entire academic career could be at risk if the university ever finds out ..."
                        Actor:: "But I just can't resist you ..."
                        Sex(Player, Actor)
                        cur += random(20, 40)
                        If cur < 50
                            cur => random(50, 60)
                        ElseIf cur > 100
                            cur => 100
                        EndIf
                    Else
                        Actor:: "I don't know what you're talking about. Now if you excuse me, I have some exam papers to mark."
                        "Unfortunately, Dr <Actor.name> <Actor.name_last> played dumb and ignored my offer. Oh well, at least, <Actor.he_or_she> didn't want to make a big deal of it and report my behavior ..."
                    EndIf
                Else
                    "I managed to convince <Actor2.name> to do it as after all, successful attainment of a university degree may have a big long-term impact on our financial future together."
                    Actor.show()
                    Actor2.show()
                    If Actor.hadSex() || [random(50, 75) < Actor:perversion && random(25, 75) < Actor2:attractiveness && Actor.isInterestedIn(Actor2)]
                        Actor:: "I really should be impartial, my entire academic career could be at risk if the university ever finds out ..."
                        Actor:: "But I just can't resist your <Actor2.handsome_or_beautiful> <Actor2.boyfriend_or_girlfriend> here ..."
                        Sex(Actor, Actor2)
                        masochist += 2
                        cur += random(20, 40)
                        If cur < 50
                            cur => random(50, 60)
                        ElseIf cur > 100
                            cur => 100
                        EndIf
                    Else
                        Actor:: "I don't know what you're talking about. Now if you excuse me, I have some exam papers to mark."
                        "Unfortunately, Dr <Actor.name> <Actor.name_last> played dumb and ignored my offer. Oh well, at least, <Actor.he_or_she> didn't want to make a big deal of it and report my behavior ..."
                    EndIf                
                EndIf
                "Eventually, I receive the final result for the <Actor.major> subject. I got a <cur>%."
                Actor2.hide()
                Actor.hide()
                tot => tot + cur
                Actor = getSpecific(Lecturer)  
            endWhile
            avg = tot / count
            "Having taken all the exams for the different subjects, my final average for my degree works out as <avg>%."
            If avg > 80
                "With that score, I got the highest possible degree classification. This should be one of my unique selling points to potential employers."
                jobexperience += 40
                graduate()
            ElseIf avg > 65
                "With that score, I got a decent enough degree classification. Most good job positions ask for this as a minimum so I did enough in that aspect."
                jobexperience += 25
                graduate()
            ElseIf avg > 50
                "I barely passed basically. Oh well, at least I got a degree, although I don't think it's anything to boast about in job interviews. It's also below the minimum requirement for even applying for some of the best-paying jobs."
                jobexperience += 10
                graduate()
            Else
                "Unfortunately, I failed my finals. I will have to retake them whenever they're available next."
            EndIf
        EndIf
    EndIf
    SceneEnd()