WHAT: all, -pickpocket, -go_to_the_bathroom, -sleep, -sleep_hotel, -nap, -nap_hotel, -rk_VB_small_sleep
WHERE: all, -work
WHEN: 17 - 21
WHO: Actor = getSpecific(Dating); If !Actor.isColleague() && !Actor.isBoss() && Actor:attractiontoplayer > 10 && Random(30, 200) < Actor:interpersonal
OTHER: !isWithCompanion()

    SceneStart()

        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        "I wonder what my <Actor.boyfriend_or_girlfriend> is up to this evening. I should call <Actor.him_or_her>."
        Player(Happy):: "Baby, where are you? When will you be back from work?"
        Actor:: "Work's already done, honey. But I'm having a few drinks with my colleagues at the bar next to my office. You can come join us if you want?"
        0:: "Join them"
        1:: "No thanks"
        
        If 0
        
            dressFormal()
            Actor.dress()
            dice = Random(0, 100)
            Actor2 = generatePerson()
            Actor2.dress()
            addNpcRelationship(Colleagues, Actor, Actor2)
            If dice < 30
                Actor3 = generatePerson()
                Actor3.dress()
                addNpcRelationship(Colleagues, Actor, Actor3)    
            Endif
            setBackground(bar)
            Actor.show(4)
            
            Actor(Happy):: "Baby, let me introduce you to <Actor2.name>."
            If dice < 30
                Actor(Happy):: "And this is <Actor3.name>."
                Actor3.show(2)
                Actor3(Happy):: "Hey there. Good to meet you at last."
            Endif
            Actor2.show(3)
            Actor2(Happy):: "Nice to meet you finally, <Player.name>. <Actor.name> couldn't stop talking about you even at work."
            Player(Happy):: "Nice to meet you too."
            
            Actor.hide()
            Actor2.hide()
            "It's getting late, we should probably head home. But where is <Actor.name>?"
            "<Actor.He_or_She> must have gone to the bathroom, let's go check it out."
            "Hmm, there's strange noise coming out of the bathroom ..."
            0:: "Peep"
            1:: "Leave"
            
            If 0
                
                arousal += 1
                perversion += 1
                setBackground(toilets)
                If isModEnabled(vin_NTR) && Actor.isInterestedIn(Actor2) && [Random(50, 100) < Actor:perversion || openRelationship()]
                
                    Sex(Actor, Actor2)
                    Player.show(0)
                    Actor.show(2)
                    Actor.show(3)
                    "My <Actor.boyfriend_or_girlfriend> is fucking <Actor.his_or_her> colleague! Not sure how I feel about this?"
                    0:: "Join them"
                    1:: "Continue watching"
                    2:: "Get angry"
                    
                    If 0
                    
                        Player(Flirty):: "Mind if I join?"
                        Sex(Player, Actor2, Actor)
                        If HungerEnable > 0
                            Player.modifyActorVar(rk_VB_Hunger, -3)
                        EndIf
                        perversion += 1
                        
                    Elseif 1
                    
                        Sex(Actor, Actor2)
                        If HungerEnable > 0
                            Player.modifyActorVar(rk_VB_Hunger, -2)
                        EndIf
                        arousal += 1
                        perversion += 1
                        
                    Else
                    
                        Player(Angry):: "How dare you! Did you invite me over here to make me watch you cheat on me? How long have you been fucking at work?"
                        Actor:attractiontoplayer -= 20
                        Actor:rapportwithplayer -= 10
                        mood -= 10
                        
                    Endif
                    
                Else
                
                    Ran = generatePersonTemporary()
                    Ran2 = generatePersonTemporary()
                    Sex(Ran, Ran2)
                    Ran.hide()
                    Ran2.hide()
                    Player.show(0)
                    "Thanks fuck for that, it's just a random couple. I thought it might be <Actor.boyfriend_or_girlfriend> cheating on me for a moment there."
                    0:: arousal > 25 ::"Join them"
                    1:: "Continue watching"
                    2:: "Leave"
                    
                    If 0
                    
                        Player(Flirty):: "Mind if I join?"
                        Sex(Player, Ran2, Ran)
                        If HungerEnable > 0
                           Player.modifyActorVar(rk_VB_Hunger, -3)
                        EndIf
                        perversion += 1
                        
                    Elseif 1
                    
                        Sex(Ran, Ran2)
                        If HungerEnable > 0
                           Player.modifyActorVar(rk_VB_Hunger, -1)
                        EndIf
                        arousal += 1
                        perversion += 1
                        
                    Endif
                    
                    setBackground(bar)
                    "A while later ..."
                    Actor.show(2)
                    Player(Happy):: "There you are, where did you go?"
                    Actor(Happy):: "I went out for a cigarette. Let's go?"         
                    
                Endif
                
            Else
            
                "A while later ..."
                Actor.show(2)
                Player(Happy):: "There you are, where did you go?"
                Actor(Happy):: "I went out for a cigarette. Let's go?"
                
            Endif
            
        Else
        
            Player(Happy):: "That's okay, baby. You guys have fun!"
            
        Endif
        
    SceneEnd() // Improved by Raddeck
    timeout(500, dating_colleague, dating_boss)