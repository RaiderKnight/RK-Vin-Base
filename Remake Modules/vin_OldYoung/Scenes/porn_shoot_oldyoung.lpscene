WHAT: all
WHERE: all, -work
WHEN: 8 - 18
WHO: none
OTHER: Random(1, 150) < pornfame && [likes_older > 30 || likes_older < -30]

    SceneStart()
        porn_shoot_oldyoung = rk_VB_porn_shoot_oldyoung.getGlobal()
        Hunger = Player.getActorVar(rk_VB_Hunger)
        HungerEnable = rk_VB_EnableHunger.getGlobal()
        UrgeToPeeEnable = rk_VB_UrgeToPeeEnable.getGlobal()
        UrgeToPee = Player.getActorVar(rk_VB_UrgeToPee)
        CashFlow = Random(200, 500)
        CashFlowloc = CashFlow.convertToLocalCurrency()
        Bonu = attractiveness + pornfame
        sito = Bonu/2.88
        BDice = Random(600, 2000)
        Dice = BDice + sito
        P1C = Dice.convertToLocalCurrency(true)
        P2 = Dice/2
        P2C = P2.convertToLocalCurrency(true)
        P3 = Dice/3
        P3C = P3.convertToLocalCurrency(true)
        Actor = generatePersonTemporary()
        While !Player.isInterestedIn(Actor)
            Actor = generatePersonTemporary()
        Endwhile
        Actor.makeInterested(Player)
        Actor:age => age + likes_older*0.5
        Actor:age += Random(-5, 5)
        If Actor:age < 18
            Actor:age => 18
        Endif
        Actor.randomizeFace()
        Actor.randomizeHairs()
        Actor.dress()
        Actor.show(2)
        Actor2 = generatePersonTemporary()
        Actor2:age => Random(35, 55)
        Actor2.randomizeFace()
        Actor2.randomizeHairs()
        Actor2.dress()
        Actor2.hide()
        DoneBeast = Player.getActorVar(tag_beastporn)
        DirAccepted = false
        Accept1 = false
        Accept2 = false
        Accept3 = false
        OfferRefuse = false
        If DoneBeast > 0 && Random(0, 100) < 50
            "One of the most famous porn directors was planning to shoot me, but pulled out in the last minute after finding out about me being involved in a bestiality shoot earlier in my career."
            "<Actor2.He_or_She> didn't think I would fit the glamcore market."
            pornfame -= 0.25
        Else
            If porn_shoot_oldyoung == 0
                "A porn director wants to book me for a old-young fetish scene with <Actor.name>, who is <Actor:age> years old!"
                "It's a well-paid offer since it's a porn niche high in demand. <Actor2.He_or_She> offer me <P2C>."
                "I guess a lot of people want to watch two pornstars with a big age gap going at it ..."
                "With all that in mind, what should I do about this offer?"
            Else
                Actor2(Serene)::"Ok, I offer you <P2C>, so do you accept or not?"
            EndIf
            0:: "Turn down the offer"
            1:: "Rip the director off (<P1C>)"
            2:: "Offer a fair price (<P3C>)"
            3:: "Give the director a bargain (<CashFlowloc>)"
            
            If 0
                Player(Sad):: "Sorry, but I have a holiday planned on the day of the shoot ..."
            Elseif 1
                If random(1, 300) < 150 + Bonu
                    DirAccepted = true
                    Accept1 = true
                Else
                    OfferRefuse = true
                EndIf
            Elseif 2
                If random(1, 300) < 100 + Bonu
                    DirAccepted = true
                    Accept2 = true
                Else
                    OfferRefuse = true
                EndIf
            Elseif 3
                DirAccepted = true
                Accept3 = true
            EndIf
            If DirAccepted
                "The porn director accepted my demands and soon enough we were meeting at the shoot location that we agreed on."
                Random
                    setBackground(home)
                    setBackground(bar)
                    setBackground(hairdresser)
                    setBackground(cafe)
                    setBackground(clothes)
                    setBackground(dentist)
                    setBackground(doctor)
                    setBackground(hotel)
                    setBackground(laundry)
                    setBackground(marketplace)
                    setBackground(musical_instrument)
                    setBackground(nightclub)
                    setBackground(work)
                    setBackground(place_of_worship)
                    setBackground(police)
                    setBackground(prison)
                    setBackground(pub)
                    setBackground(restaurant)
                    setBackground(shoes)
                    setBackground(sports_centre)
                    setBackground(supermarket)
                    setBackground(theatre)
                    setBackground3D(Modules/rk_vin_Base/World/toilets___vin01.lpworld)
                EndRandom
                Actor2.dress()
                Actor2.show(3)
                Actor.strip()
                "What should I do?"
                0:: "Shoot the scene as agreed"
                1:: "Invite the porn director to join for a threesome."
                
                If 0
                    Sex(Actor, Player)
                    If HungerEnable > 0
                        Player.modifyActorVar(rk_VB_Hunger, -2)
                    EndIf
                    pornfame += 0.5
                    perversion += 0.25
                Else
                    Player(Flirty):: "I heard you were quite a star performer back in your day? Why don't you join us, make this a POV threesome video and teach us a thing or two about performing in this industry?"
                    Player(Flirty):: "It would be quite a honour to star alongside an industry legend."
                    Actor2(Flirty):: "It's been a long time since I last performed, but since you insist ..."
                    Sex(Actor, Actor2, Player)
                    If HungerEnable > 0
                        Player.modifyActorVar(rk_VB_Hunger, -3)
                    EndIf
                    pornfame += 1
                    perversion += 0.5
                Endif
                If Accept1
                    money += Dice
                ElseIf Accept2
                    money += P3
                ElseIf Accept3  
                    money += CashFlow
                Endif
                rk_VB_porn_shoot_oldyoung.setGlobal(0)
            Endif
            If OfferRefuse
                Actor2(Furious):: "No way, that's way above the market rate for this type of shoots! You have too high an opinion of yourself, honey!"
                Actor2(Furious):: "You should ask the other pornstars how much they charge."
                0:: "Say nothing"
                1:: "Cheap motherfucker!"
                
                If 1
                    If Random(0, 100) < 30
                        "The director got pissed off and tried to hit me"
                        If Random(0, 100) < martial
                            "Unfortunately for <Actor2.him_or_her>, I was no pushover and gave <Actor2.him_or_her> a good spanking."
                            "As things would have it, our little incident went viral in the porn industry media and somehow made me even more popular."
                            "I guess no publicity is bad publicity. Besides, other directors will have to think twice now before trying to bully me in the future."
                            pornfame += Random(2, 5)
                        Else
                            "I tried to stand my ground but ..."
                            "Unfortunately for me, <Actor2.he_or_she> was a powerful <Actor2.man_or_woman> and I got quite seriously hurt."
                            "As things would have it, our little incident went viral in the porn industry media and somehow made me a butt of jokes. No doubt this would affect my future prospects with other directors."
                            pornfame -= Random(1, 3)               
                        Endif
                        rk_VB_porn_shoot_oldyoung.setGlobal(0)
                    Elseif Random(0, 100) < 50
                        Actor2(Furious)::"How dare you? You'll never work for this studio again!"
                        pornfame -= 1
                        rk_VB_porn_shoot_oldyoung.setGlobal(0)
                    Else
                        If porn_shoot_oldyoung == 1
                            Actor2(Sad)::"Sorry but that's a final no! I don't have time to deal anymore, I have a movie to create!"
                        Else
                            Actor2(Serene)::"Fine, I changed my mind. Let's try negotiating again ..."
                            rk_VB_porn_shoot_oldyoung.setGlobal(1)
                            followUp(porn_shoot_oldyoung)
                        Endif
                    Endif
                Endif
            Endif
        Endif
    SceneEnd() // Improved by Raddeck
    timeout(48, porn_shoot_oldyoung)